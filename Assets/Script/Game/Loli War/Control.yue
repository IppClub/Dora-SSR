--[[ Copyright (c) 2024 Li Jin, dragon-fly@qq.com

Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE. ]]

_ENV = Dora dora.Platformer
import "UI.Control.HPWheel"
import "UI.Control.Basic.AlignNode"
import "UI.View.LeftTouchPad"
import "UI.View.RightTouchPad"
import "UI.View.RestartPad"
import "UI.Control.StartPanel"

store: Store = Data
:GroupPlayer = Store

playerGroup = Group ["hero", "unit"]
updatePlayerControl = (key, flag) ->
	playerGroup\each => @unit.data[key] = flag if @group == GroupPlayer

with root = AlignNode isRoot: true
	\addChild HPWheel!
	switch App.platform
		when "iOS", "Android"
			\addChild with LeftTouchPad!
				\slot "KeyLeftUp", -> updatePlayerControl "keyLeft", false
				\slot "KeyLeftDown", -> updatePlayerControl "keyLeft", true
				\slot "KeyRightUp", -> updatePlayerControl "keyRight", false
				\slot "KeyRightDown", -> updatePlayerControl "keyRight", true
			\addChild with RightTouchPad!
				\slot "KeyFUp", -> updatePlayerControl "keyF", false
				\slot "KeyFDown", -> updatePlayerControl "keyF", true
				\slot "KeyUpUp", -> updatePlayerControl "keyUp", false
				\slot "KeyUpDown", -> updatePlayerControl "keyUp", true
		when "macOS", "Windows", "Linux"
			\addChild with Node!
				\schedule ->
					updatePlayerControl "keyLeft", Keyboard\isKeyPressed "A"
					updatePlayerControl "keyRight", Keyboard\isKeyPressed "D"
					updatePlayerControl "keyUp", Keyboard\isKeyPressed "K"
					updatePlayerControl "keyF", Keyboard\isKeyPressed "J"
	showStartPanel = ->
		root\addChild with StartPanel!
			\slot "AlignLayout", (w) ->
				w *= 0.6
				width = 210 * 2 * App.devicePixelRatio
				.scaleX = .scaleY = w / width if w < width
		root\alignLayout!
	\addChild with RestartPad!
		\slot "Tapped", ->
			Store.winner = -1
			showStartPanel!
	\addTo Director.ui
	showStartPanel!
