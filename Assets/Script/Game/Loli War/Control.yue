--[[ Copyright (c) 2024 Li Jin, dragon-fly@qq.com

Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE. ]]

_ENV = Dora Dora.Platformer
import "UI.Control.HPWheel"
import "UI.View.LeftTouchPad"
import "UI.View.RightTouchPad"
import "UI.View.RestartPad"
import "UI.Control.StartPanel"
import "InputManager"

:Trigger = InputManager

inputManager = InputManager.CreateManager
	* name: "Select"
		actions:
			* name: "Flandre"
				trigger: Trigger.Selector [
					Trigger.ButtonDown "dpleft"
					Trigger.KeyDown "A"
				]
			* name: "Villy"
				trigger: Trigger.Selector [
					Trigger.ButtonDown "dpdown"
					Trigger.KeyDown "S"
				]
			* name: "Dorothy"
				trigger: Trigger.Selector [
					Trigger.ButtonDown "dpright"
					Trigger.KeyDown "D"
				]
	* name: "Control"
		actions:
			* name: "LeftDown"
				trigger: Trigger.Selector [
					Trigger.ButtonDown "dpleft"
					Trigger.KeyDown "A"
				]
			* name: "RightDown"
				trigger: Trigger.Selector [
					Trigger.ButtonDown "dpright"
					Trigger.KeyDown "D"
				]
			* name: "JumpDown"
				trigger: Trigger.Selector [
					Trigger.ButtonDown "a"
					Trigger.KeyDown "K"
				]
			* name: "AttackDown"
				trigger: Trigger.Selector [
					Trigger.ButtonDown "b"
					Trigger.KeyDown "J"
				]
			* name: "LeftUp"
				trigger: Trigger.Selector [
					Trigger.ButtonUp "dpleft"
					Trigger.KeyUp "A"
				]
			* name: "RightUp"
				trigger: Trigger.Selector [
					Trigger.ButtonUp "dpright"
					Trigger.KeyUp "D"
				]
			* name: "JumpUp"
				trigger: Trigger.Selector [
					Trigger.ButtonUp "a"
					Trigger.KeyUp "K"
				]
			* name: "AttackUp"
				trigger: Trigger.Selector [
					Trigger.ButtonUp "x"
					Trigger.KeyUp "J"
				]
			* name: "Restart"
				trigger: Trigger.Selector [
					Trigger.ButtonDown "back"
					Trigger.KeyDown "Q"
				]

inputManager\pushContext "Control"

store: Store = Data
:GroupPlayer = Store

playerGroup = Group ["hero", "unit"]
updatePlayerControl = (key, flag) ->
	playerGroup\each => @unit.data[key] = flag if @group == GroupPlayer

showStartPanel = ->
	Director.ui\addChild with AlignNode true
		\css 'align-items: center; justify-content: center'
		\addChild with align = AlignNode!
			\css 'width: 80%; height: 80%'
			\addChild with StartPanel!
				align\slot "AlignLayout", (w, h) ->
					.position = Vec2 w / 2, h / 2
					.scaleX = .scaleY = math.min w / .node.width, h / .node.height

inputNode = with inputManager\getNode!
	\gslot "Input.LeftDown", -> updatePlayerControl "keyLeft", true
	\gslot "Input.LeftUp", -> updatePlayerControl "keyLeft", false
	\gslot "Input.RightDown", -> updatePlayerControl "keyRight", true
	\gslot "Input.RightUp", -> updatePlayerControl "keyRight", false
	\gslot "Input.JumpDown", -> updatePlayerControl "keyUp", true
	\gslot "Input.JumpUp", -> updatePlayerControl "keyUp", false
	\gslot "Input.AttackDown", -> updatePlayerControl "keyF", true
	\gslot "Input.AttackUp", -> updatePlayerControl "keyF", false
	\gslot "Input.Restart", ->
		Store.winner = -1
		showStartPanel!
	\gslot "InputManager.Select", (on) -> if on
		inputManager\pushContext "Select"
	else
		inputManager\popContext!

with root = AlignNode true
	\css 'flex-direction: column; justify-content: space-between'
	\addChild with AlignNode!
		\css 'width: 10; height: 10; margin-top: 50; margin-left: 60'
		\addChild HPWheel!
	\addChild with AlignNode!
		\css 'margin: 0, 10, 40; height: 104; flex-direction: row; justify-content: space-between'
		switch App.platform
			when "iOS", "Android"
				\addChild with AlignNode!
					\css 'height: 104; width: 0'
					\addChild with LeftTouchPad!
						\slot "KeyLeftUp", -> inputManager\emitKeyUp "A"
						\slot "KeyLeftDown", -> inputManager\emitKeyDown "A"
						\slot "KeyRightUp", -> inputManager\emitKeyUp "D"
						\slot "KeyRightDown", -> inputManager\emitKeyDown "D"
				\addChild with AlignNode!
					\css 'height: 104; width: 0'
					\addChild with RightTouchPad!
						\slot "KeyFUp", -> inputManager\emitKeyUp "J"
						\slot "KeyFDown", -> inputManager\emitKeyDown "J"
						\slot "KeyUpUp", -> inputManager\emitKeyUp "K"
						\slot "KeyUpDown", -> inputManager\emitKeyDown "K"
	\addChild with RestartPad!
		root\slot "AlignLayout", (w, h) ->
			.position = Vec2 w - 10, h - 10
		\slot "Tapped", ->
			Store.winner = -1
			showStartPanel!
	\addTo Director.ui
	showStartPanel!
