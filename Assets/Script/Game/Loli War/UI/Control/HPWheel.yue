--[[ Copyright (c) 2016-2025 Li Jin <dragon-fly@qq.com>

Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE. ]]

_ENV = Dora Dora.Platformer
import "UI.View.HPWheel"
import "UI.View.EPHint"

{
	:GroupPlayer
	:GroupEnemy
	:MaxEP
	:MaxHP
} = Data.store

export default Class HPWheel,
	__init: =>
		@ep = MaxEP
		@hp = MaxHP
		@hints = {}

		@hpShow\slot "AnimationEnd", (name) ->
			@hpShow\play "idle", true if name == "hit"

		@gslot "HPChange", (group, value) ->
			if group == GroupPlayer
				newHP = math.max @hp + value, 0
				@hp = math.floor math.max(math.min(MaxHP, newHP), 0) + 0.5
				@hpShow.look = tostring @hp
				@hpShow\play "hit" if value < 0

		@gslot "EPChange", (group, value) ->
			if group == GroupPlayer
				switch value
					when 1, -1, -2, 6
						@ep = math.floor math.max(math.min(MaxEP, @ep + value), 0) + 0.5
						@fill\perform ScaleX 0.2, @fill.scaleX, @ep / MaxEP
						hint = with EPHint index: #@hints + 1, clip: string.format "%+d", value
							.index = #@hints + 1
							\slot "DisplayEnd", ->
								index = hint.index
								hint\removeFromParent!
								table.remove @hints, index
								for i, v in ipairs @hints
									v\runAction X 0.2, v.x, 55 + 25 * (i - 1)
									v.index = i
						table.insert @hints, hint
						@energy\addChild hint

		@gslot "BlockValue", (group, value) ->
			switch group
				when GroupPlayer
					@playerBlocks.value = value
				when GroupEnemy
					@enemyBlocks.value = value

		@gslot "BlockChange", (group, value) ->
			switch group
				when GroupPlayer
					@playerBlocks.value = math.max @playerBlocks.value + value, 0
				when GroupEnemy
					@enemyBlocks.value = math.max @enemyBlocks.value + value, 0
