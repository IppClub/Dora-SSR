_ENV = Dorothy builtin.Platformer

groundEntranceEnd = (name,playable)->
	return unless name == "groundEntrance"
	playable.parent\stop!

UnitAction\add "groundEntrance",
	priority: 6
	reaction: -1
	recovery: 0
	queued: true
	available: -> true
	create: =>
		@entity.lastGroup = @group
		@group = 0
		with @playable
			.speed = 1
			\slot "AnimationEnd",groundEntranceEnd
			\play "groundEntrance"
		-> false
	stop: =>
		@playable\slot("AnimationEnd")\remove groundEntranceEnd
		@group = @entity.lastGroup
		@entity.lastGroup = nil
		@entity.entered = true

UnitAction\add "fallOff",
	priority: 1
	reaction: 1
	recovery: 0
	available: => not @onSurface
	create: =>
		if @velocityY <= 0
			@entity.fallDown = true
			with @playable
				.speed = 1
				\play "fallOff"
		else @entity.fallDown = false
		(action)=>
			return true if @onSurface
			if not @entity.fallDown and
				@playable.current ~= "fallOff" and
				@velocityY <= 0
				@entity.fallDown = true
				with @playable
					.speed = 1
					\play "fallOff"
			false
