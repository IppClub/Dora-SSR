--[[ Copyright (c) 2016-2025 Li Jin <dragon-fly@qq.com>

Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE. ]]

_ENV = Dora Dora.Platformer
import "UI.Control.Basic.CircleButton"

store: Store = Data

Store.controlPlayer = "KidW"
playerGroup = Group ["player",]
updatePlayerControl = (key, flag) ->
	if player := playerGroup\find => @unit.tag == Store.controlPlayer
		player.unit.data[key] = flag

with AlignNode true
	\css 'flex-direction: column-reverse'
	.visible = false
	\addChild with AlignNode!
		\css "
			width: auto;
			height: 60;
			margin: 0, 20, 40;
			flex-direction: row;
			justify-content: space-between
		"
		\addChild with AlignNode!
			\css 'height: 60; width: 0'
			\addChild with Menu!
				.anchor = Vec2.zero
				.size = Size 130, 60
				\addChild with CircleButton {
						text: "Left"
						radius: 60
						fontSize: math.floor 36
					}
					.scaleX = .scaleY = 0.5
					.anchor = Vec2.zero
					\slot "TapBegan", -> updatePlayerControl "keyLeft", true
					\slot "TapEnded", -> updatePlayerControl "keyLeft", false
				\addChild with CircleButton {
						text: "Right"
						x: 70
						radius: 60
						fontSize: math.floor 36
					}
					.scaleX = .scaleY = 0.5
					.anchor = Vec2.zero
					\slot "TapBegan", -> updatePlayerControl "keyRight", true
					\slot "TapEnded", -> updatePlayerControl "keyRight", false
		\addChild with AlignNode!
			\css 'height: 60; width: 0'
			\addChild with Menu!
				.anchor = Vec2 1, 0
				.size = Size 130, 60
				\addChild with CircleButton {
						text: "Jump"
						radius: 60
						fontSize: math.floor 36
					}
					.scaleX = .scaleY = 0.5
					.anchor = Vec2.zero
					\slot "TapBegan", -> updatePlayerControl "keyUp", true
					\slot "TapEnded", -> updatePlayerControl "keyUp", false
				\addChild with CircleButton {
						text: "Shoot"
						x: 70
						radius: 60
						fontSize: math.floor 36
					}
					.scaleX = .scaleY = 0.5
					.anchor = Vec2.zero
					\slot "TapBegan", -> updatePlayerControl "keyShoot", true
					\slot "TapEnded", -> updatePlayerControl "keyShoot", false
	\addTo with Director.ui
		.renderGroup = true

Store.keyboardEnabled = false
keyboardControl = ->
	return unless Store.keyboardEnabled
	updatePlayerControl "keyLeft", Keyboard\isKeyPressed "A"
	updatePlayerControl "keyRight", Keyboard\isKeyPressed "D"
	updatePlayerControl "keyUp", Keyboard\isKeyPressed "K"
	updatePlayerControl "keyShoot", Keyboard\isKeyPressed "J"

with Node!
	\schedule keyboardControl
