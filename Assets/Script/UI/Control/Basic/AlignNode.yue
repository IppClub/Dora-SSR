_ENV = Dorothy!

export default Class Node,
	__init: (args)=>
		{
			:isRoot = false
			:inUI = true
			:hAlign = "Center"
			:vAlign = "Center"
			:alignOffset = Vec2.zero
			:alignWidth
			:alignHeight
		} = args or {}
		@inUI = inUI
		@_isRoot = isRoot
		if @_isRoot
			viewSize = inUI and App.bufferSize or View.size
			@size = viewSize
			@_viewSize = viewSize
			@gslot "AppSizeChanged", ->
				viewSize = @inUI and App.bufferSize or View.size
				if @_viewSize ~= viewSize
					@_viewSize = viewSize
					@size = viewSize
					:width, :height = viewSize
					@emit "AlignLayout", width, height
					@eachChild (child)->
						child\emit "AlignLayout", width, height
			@slot "Enter", ->
				:width, :height = @inUI and App.bufferSize or View.size
				@emit "AlignLayout", width, height
				@eachChild (child)->
					child\emit "AlignLayout", width, height
		else
			@hAlign = hAlign
			@vAlign = vAlign
			@alignOffset = alignOffset
			@alignWidth = alignWidth
			@alignHeight = alignHeight
			@slot "AlignLayout", (w, h)->
				env = :w,:h
				oldSize = @size
				if @alignWidth
					widthFunc = load "_ENV = Dorothy(...)\nreturn #{@alignWidth}"
					@width = widthFunc env
				if @alignHeight
					heightFunc = load "_ENV = Dorothy(...)\nreturn #{@alignHeight}"
					@height = heightFunc env
				@x = switch @hAlign
					when "Left" then @width / 2 + @alignOffset.x
					when "Center" then w / 2 + @alignOffset.x
					when "Right" then w - @width / 2 - @alignOffset.x
				@y = switch @vAlign
					when "Bottom" then @height / 2 + @alignOffset.y
					when "Center" then h / 2 + @alignOffset.y
					when "Top" then h - @height / 2 - @alignOffset.y
				newSize = @size
				if oldSize ~= newSize
					:width, :height = newSize
					@eachChild (child)->
						child\emit "AlignLayout", width, height

	alignLayout:=>
		if @_isRoot
			:width, :height = @inUI and App.bufferSize or View.size
			@emit "AlignLayout", width, height
			@eachChild (child)->
				child\emit "AlignLayout", width, height
		else
			:width, :height = @size
			@eachChild (child)->
				child\emit "AlignLayout", width, height
