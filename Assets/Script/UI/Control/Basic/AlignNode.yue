_ENV = Dorothy!

export default Class Node,
	__init:(args)=>
		{
			:isRoot, :inUI,
			:hAlign, :vAlign,
			:alignOffset,
			:alignWidth, :alignHeight
		} = args or {}
		@inUI = inUI or true
		@_isRoot = isRoot or false
		@hAlign = hAlign or "Center"
		@vAlign = vAlign or "Center"
		@alignOffset = alignOffset or Vec2.zero
		@alignWidth = alignWidth
		@alignHeight = alignHeight
		if @_isRoot
			viewSize = View.size
			@size = viewSize
			@_viewSize = viewSize
			@position = Vec2(0.5,0.5) * viewSize if @inUI
			@gslot "AppSizeChanged",->
				viewSize = View.size
				if @_viewSize ~= viewSize
					@_viewSize = viewSize
					@size = viewSize
					@position = Vec2(0.5,0.5) * viewSize if @inUI
					@eachChild (child)->
						child\emit "AlignLayout", viewSize.width, viewSize.height
			@slot "Enter", ->
				@eachChild (child)->
					child\emit "AlignLayout", viewSize.width, viewSize.height
		else
			@slot "AlignLayout", (w, h)->
				env = :w,:h
				oldSize = @size
				if @alignWidth
					widthFunc = load "_ENV = ...\nreturn #{@alignWidth}"
					@width = widthFunc env
				if @alignHeight
					heightFunc = load "_ENV = ...\nreturn #{@alignHeight}"
					@height = heightFunc env
				@x = switch @hAlign
					when "Left" then @width/2 + @alignOffset.x
					when "Center" then w/2 + @alignOffset.x
					when "Right" then w - @width/2 - @alignOffset.x
				@y = switch @vAlign
					when "Bottom" then @height/2 + @alignOffset.y
					when "Center" then h/2 + @alignOffset.y
					when "Top" then h - @height/2 - @alignOffset.y
				newSize = @size
				if oldSize ~= newSize
					@eachChild (child)->
						child\emit "AlignLayout", newSize.width, newSize.height

	alignLayout:=>
		if @_isRoot
			viewSize = View.size
			@eachChild (child)->
				child\emit "AlignLayout", viewSize.width, viewSize.height
		else
			size = @size
			@eachChild (child)->
				child\emit "AlignLayout", size.width, size.height
