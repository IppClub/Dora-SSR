--[[ Copyright (c) 2016-2025 Li Jin <dragon-fly@qq.com>

Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE. ]]

_ENV = Dora Dora.ImGui
import "ImGuiEnum" as :$WindowFlag, :$SetCond
const :Content, :Path = Dora
const type = type

App.idled = true
ShowConsole true

moduleCache = []
oldRequire = _G.require
const require = (path) ->
	loaded = package.loaded[path]
	if loaded == nil
		moduleCache[] = path
		return oldRequire path
	loaded
_G.require = require
Dora.require = require

:searchPaths = Content

useChinese = App.locale\match("^zh")?
updateLocale = ->
	useChinese = App.locale\match("^zh")?
	searchPaths[#searchPaths] = Path Content.assetPath, "Script", "Lib", "Dora", useChinese and "zh-Hans" or "en"
	Content.searchPaths = searchPaths

isDesktop = App.platform in ["Windows", "macOS", "Linux"]

if DB\exist "Config"
	switch DB\query "select value_str from Config where name = 'locale'"
		when [ [locale,], ] then if App.locale ~= locale
			App.locale = locale
			updateLocale!
	if isDesktop
		switch DB\query "select value_str from Config where name = 'writablePath'"
			when [ [writablePath,], ]
				Content.writablePath = writablePath

import "Config"

config = Config(
	""
	"fpsLimited"
	"targetFPS"
	"fixedFPS"
	"vsync"
	"fullScreen"
	"alwaysOnTop"
	"winX"
	"winY"
	"winWidth"
	"winHeight"
	"themeColor"
	"locale"
	"editingInfo"
	"showStats"
	"showConsole"
	"showFooter"
	"filter"
	"engineDev"
	"webProfiler"
	"drawerWidth"
	"lastUpdateCheck"
	"updateNotification"
	"writablePath"
	"webIDEConnected"
)
config\load!

unless config.writablePath?
	config.writablePath = Content.appPath

unless config.webIDEConnected?
	config.webIDEConnected = false

if config.fpsLimited?
	App.fpsLimited = config.fpsLimited
else
	config.fpsLimited = App.fpsLimited

if config.targetFPS?
	App.targetFPS = config.targetFPS
else
	config.targetFPS = App.targetFPS

if config.vsync?
	View.vsync = config.vsync
else
	config.vsync = View.vsync

if config.fixedFPS?
	Director.scheduler.fixedFPS = config.fixedFPS
else
	config.fixedFPS = Director.scheduler.fixedFPS

showEntry = false
thread ->
	sleep!
	sleep!
	showEntry = true

isDesktop = false
if App.platform in ["Linux", "Windows", "macOS"]
	isDesktop = true
	if config.fullScreen
		App.fullScreen = true
	elseif config.winWidth? and config.winHeight?
		size = Size config.winWidth, config.winHeight
		if App.winSize ~= size
			App.winSize = size
		x: winX, y: winY = App.winPosition
		if config.winX?
			winX = config.winX
		else
			config.winX = -1
		if config.winY?
			winY = config.winY
		else
			config.winY = -1
		App.winPosition = Vec2 winX, winY
	if config.alwaysOnTop?
		App.alwaysOnTop = config.alwaysOnTop
	else
		config.alwaysOnTop = true

if config.themeColor?
	App.themeColor = Color config.themeColor
else
	config.themeColor = App.themeColor\toARGB!

if not config.locale?
	config.locale = App.locale

showStats = false
if config.showStats?
	showStats = config.showStats
else
	config.showStats = showStats

showConsole = false
if config.showConsole?
	showConsole = config.showConsole
else
	config.showConsole = showConsole

showFooter = true
if config.showFooter?
	showFooter = config.showFooter
else
	config.showFooter = showFooter

filterBuf = Buffer 20
if config.filter?
	filterBuf.text = config.filter
else
	config.filter = ""

engineDev = false
if config.engineDev?
	engineDev = config.engineDev
else
	config.engineDev = engineDev

if config.webProfiler?
	Director.profilerSending = config.webProfiler
else
	config.webProfiler = true
	Director.profilerSending = true

unless config.drawerWidth?
	config.drawerWidth = 200

export.getConfig = -> config
export.getEngineDev = ->
	return false unless App.debugging
	config.engineDev

export.connectWebIDE = ->
	unless config.webIDEConnected
		config.webIDEConnected = true
		if App.platform in ["Windows", "Linux", "macOS"]
			ratio = App.winSize.width / App.visualSize.width
			App.winSize = Size 640 * ratio, 480 * ratio

updateCheck = -> thread ->
	if res := HttpClient\getAsync "https://api.github.com/repos/IppClub/Dora-SSR/releases/latest"
		if data := json.load res
			major, minor, patch = App.version\match "(%d+)%.(%d+)%.(%d+)%.(%d+)"
			a, b, c = tonumber(major), tonumber(minor), tonumber patch
			sa, sb, sc = data.tag_name\match "v(%d+)%.(%d+)%.(%d+)"
			na, nb, nc = tonumber(sa), tonumber(sb), tonumber sc
			if na < a
				goto not_new_version
			if na == a
				if nb < b
					goto not_new_version
				if nb == b
					if nc < c
						goto not_new_version
					if nc == c
						goto not_new_version
			config.updateNotification = true
			::not_new_version::
			config.lastUpdateCheck = os.time!

if config.lastUpdateCheck?
	diffSeconds = os.difftime os.time!, config.lastUpdateCheck
	if diffSeconds >= 7 * 24 * 60 * 60
		updateCheck!
else
	updateCheck!

from "Utils" import Set, Struct, LintYueGlobals, GSplit
options: {extension: yueext} = yue

SetDefaultFont "sarasa-mono-sc-regular", 20

building = false

export getAllFiles = (path, exts, recursive = true) ->
	filters = Set exts
	files = if recursive
		Content\getAllFiles path
	else
		Content\getFiles path
	return for file in *files
		continue if not filters[Path\getExt file]
		file

getFileEntries = (path, recursive = true, excludeFiles = nil) ->
	entries = {}
	excludes = if excludeFiles
		Set excludeFiles
	for file in *getAllFiles path, ["lua", "xml", yueext, "tl"], recursive
		entryName = Path\getName file
		if excludes and excludes[entryName]
			continue
		fileName = Path\replaceExt file, ""
		fileName = Path path, fileName
		entryAdded = for [ename, efile] in *entries
			break true if entryName == ename and efile == fileName
		continue if entryAdded
		entry = [entryName, fileName]
		entries[] = entry
	table.sort entries, (a, b) -> a[1] < b[1]
	entries

getProjectEntries = (path) ->
	entries = []
	for dir in *Content\getDirs path
		continue if dir\match "^%."
		for file in *getAllFiles Path(path, dir), ["lua", "xml", yueext, "tl", "wasm"]
			if "init" == Path\getName(file)\lower!
				fileName = Path\replaceExt file, ""
				fileName = Path path, dir, fileName
				entryName = Path\getName Path\getPath fileName
				entryAdded = for [ename, efile] in *entries
					break true if entryName == ename and efile == fileName
				continue if entryAdded
				examples = []
				tests = []
				examplePath = Path path, dir, Path\getPath(file), "Example"
				if Content\exist examplePath
					for [name, ePath] in *getFileEntries examplePath
						entry = {
							name, Path path, dir, Path\getPath(file), ePath
							workDir: Path\getPath fileName
						}
						examples[] = entry
				testPath = Path path, dir, Path\getPath(file), "Test"
				if Content\exist testPath
					for [name, tPath] in *getFileEntries testPath
						entry = {
							name, Path path, dir, Path\getPath(file), tPath
							workDir: Path\getPath fileName
						}
						tests[] = entry
				entry = [entryName, fileName, examples, tests]
				bannerFile = Path path, Path\getPath(fileName), "Image", "banner.png"
				unless Content\exist bannerFile
					bannerFile = Path path, Path\getPath(fileName), "Image", "banner.jpg"
					bannerFile = nil unless Content\exist bannerFile
				if bannerFile then thread ->
					if Cache\loadAsync bannerFile
						bannerTex = Texture2D bannerFile
						if bannerTex
							entry[] = bannerFile
							entry[] = bannerTex
				entries[] = entry
	table.sort entries, (a, b) -> a[1] < b[1]
	entries

local gamesInDev
local doraTools
local allEntries

updateEntries = ->
	gamesInDev = getProjectEntries Content.writablePath
	doraTools = getFileEntries Path(Content.assetPath, "Script", "Tools"), false

	allEntries = []
	for game in *gamesInDev
		allEntries[] = game
		[_, _, examples, tests] = game
		for example in *examples
			allEntries[] = example
		for test in *tests
			allEntries[] = test

updateEntries!

doCompile = (minify) ->
	return if building
	building = true
	startTime = App.runningTime
	luaFiles = []
	yueFiles = []
	xmlFiles = []
	tlFiles = []
	:writablePath = Content
	buildPaths = [
		[
			Content.assetPath
			Path writablePath, ".build"
			""
		],
	]
	for [_, entryFile] in *gamesInDev
		gamePath = Path\getPath Path\getRelative entryFile, writablePath
		buildPaths[] =
			* Path writablePath, gamePath
			* Path writablePath, ".build", gamePath
			* Path(writablePath, gamePath, "Script", "?.lua") .. ";" .. Path writablePath, gamePath, "?.lua"
			* gamePath
	for [inputPath, outputPath, searchPath, gamePath] in *buildPaths
		continue unless Content\exist inputPath

		for file in *getAllFiles inputPath, ["lua",]
			luaFiles[] =
				* file
				* Path inputPath, file
				* Path outputPath, file
				* gamePath

		for file in *getAllFiles inputPath, [yueext,]
			yueFiles[] =
				* file
				* Path inputPath, file
				* Path outputPath, Path\replaceExt file, "lua"
				* searchPath
				* gamePath

		for file in *getAllFiles inputPath, ["xml",]
			xmlFiles[] =
				* file
				* Path inputPath, file
				* Path outputPath, Path\replaceExt file, "lua"
				* gamePath

		for file in *getAllFiles inputPath, ["tl",]
			if not file\match ".*%.d%.tl$"
				tlFiles[] =
					* file
					* Path inputPath, file
					* Path outputPath, Path\replaceExt file, "lua"
					* searchPath
					* gamePath

	paths = {Path\getPath(file[3]), true \
		for files in *[luaFiles, yueFiles, xmlFiles, tlFiles] \
		for file in *files}

	Content\mkdir path for path in pairs paths

	totalFiles = #yueFiles + #xmlFiles + #tlFiles
	fileCount = 0
	errors = []
	for [file, input, output, searchPath, gamePath] in *yueFiles
		filename = if gamePath then Path gamePath, file else file
		(success) <- yue.compile input, output, searchPath, (codes, err, globals) ->
			if not codes
				errors[] = "Compile errors in #{filename}.\n#{err}"
				return
			success, result = LintYueGlobals codes, globals
			if success
				codes = codes\gsub "%s*local%s*_ENV%s*=%s*Dora%([^%)]-%)[^\n\r]+[\n\r%s]*", "\n"
				codes = codes\gsub "%s*local%s*_ENV%s*=%s*Dora[^%w_$][^\n\r]+[\n\r%s]*", "\n"
				codes = codes\gsub "^\n*", ""
				result ..= "\n" unless result == ""
				"-- [yue]: #{file}\n#{result}#{codes}"
			else
				if yueCodes := Content\load input
					globalErrors = []
					for [name, line, col] in *result
						countLine = 1
						code = ""
						for lineCode in yueCodes\gmatch "([^\r\n]*)\r?\n?"
							if countLine == line
								code = lineCode
								break
							countLine += 1
						globalErrors[] = "invalid global variable \"#{name}\"\nin \"#{filename}\", at line #{line}, col #{col}.\n#{code\gsub("\t", " ") .. '\n' .. string.rep(" ", col - 1) .. "^"}"
					errors[] = table.concat globalErrors, "\n"
				else
					errors[] = "failed to load file #{input}"
		print "Yue compiled: #{filename}" if success
		fileCount += 1

	thread ->
		for [file, input, output, gamePath] in *xmlFiles
			filename = if gamePath then Path gamePath, file else file
			sourceCodes = Content\loadAsync input
			codes, err = xml.tolua sourceCodes
			if not codes
				errors[] = "Compile errors in #{filename}.\n#{err}"
			else
				Content\saveAsync output, "-- [xml]: #{file}\n#{codes}"
				print "Xml compiled: #{filename}"
			fileCount += 1

	thread ->
		for [file, input, output, searchPath, gamePath] in *tlFiles
			filename = if gamePath then Path gamePath, file else file
			sourceCodes = Content\loadAsync input
			codes, err = teal.toluaAsync sourceCodes, file, searchPath
			if not codes
				errors[] = "Compile errors in #{filename}.\n#{err}"
			else
				Content\saveAsync output, codes
				print "Teal compiled: #{filename}"
			fileCount += 1

	thread ->
		wait -> fileCount == totalFiles
		if minify
			for files in *[yueFiles, xmlFiles, tlFiles] do for file in *files
				output = Path\replaceExt file[3], "lua"
				luaFiles[] =
					* Path\replaceExt file[1], "lua"
					* output
					* output

			from "luaminify" import FormatMini
			for [file, input, output] in *luaFiles
				if Content\exist input
					sourceCodes = Content\loadAsync input
					res, err = FormatMini sourceCodes
					if res
						Content\saveAsync output, res
						print "Minify: #{file}"
					else
						errors[] = "Minify errors in #{file}.\n#{err}"
				else
					errors[] = "Minify errors in #{file}.\nTarget file is not exist!"
			package.loaded["luaminify.FormatMini"] = nil
			package.loaded["luaminify.ParseLua"] = nil
			package.loaded["luaminify.Scope"] = nil
			package.loaded["luaminify.Util"] = nil
		errorMessage = table.concat errors, "\n"
		print errorMessage if errorMessage ~= ""
		builtFiles = totalFiles + (minify and #luaFiles or 0) - #errors
		print "#{builtFiles} #{builtFiles == 1 and 'file' or 'files'} built! Cost #{string.format '%.2f', App.runningTime - startTime}s"
		print "#{#errors} #{#errors == 1 and 'file failed' or 'files failed'} to build."
		Content\clearPathCache!
		teal.clear!
		yue.clear!
		building = false

doClean = ->
	return if building
	:writablePath = Content
	targetDir = Path writablePath, ".build"
	Content\clearPathCache!
	if Content\remove targetDir
		print "Cleaned: #{targetDir}"

screenScale = 2.0 -- App.devicePixelRatio
scaleContent = false
isInEntry = true
currentEntry = nil

footerWindow = nil
entryWindow = nil
testingThread = nil

setupEventHandlers = nil

export allClear = ->
	for routine in *Routine
		switch routine
			when footerWindow, entryWindow, testingThread
				continue
			else
				Routine\remove routine
	for module in *moduleCache
		package.loaded[module] = nil
	moduleCache = []
	Director\cleanup!
	Entity\clear!
	Platformer.Data\clear!
	Platformer.UnitAction\clear!
	Audio\stopStream 0.5
	Struct\clear!
	View.postEffect = nil
	View.scale = scaleContent and screenScale or 1
	Director.clearColor = Color 0xff1a1a1a
	teal.clear!
	yue.clear!
	for _, item in pairs ubox!
		node\cleanup! if node := tolua.cast item, "Node"
	collectgarbage!
	collectgarbage!
	Wasm\clear!
	thread ->
		sleep!
		Cache\removeUnused!
	setupEventHandlers!
	Content.searchPaths = searchPaths
	App.idled = true

clearTempFiles = ->
	:writablePath = Content
	Content\remove Path writablePath, ".upload"
	Content\remove Path writablePath, ".download"

waitForWebStart = true
thread ->
	sleep 2
	waitForWebStart = false

reloadDevEntry = -> thread ->
	waitForWebStart = true
	doClean!
	allClear!
	_G.require = oldRequire
	Dora.require = oldRequire
	package.loaded["Script.Dev.Entry"] = nil
	Director.systemScheduler\schedule ->
		Routine\clear!
		oldRequire "Script.Dev.Entry"
		true

setWorkspace = (path) ->
	Content.writablePath = path
	config.writablePath = Content.writablePath
	thread ->
		sleep!
		reloadDevEntry!

setupEventHandlers = ->
	with Director.postNode
		\onAppEvent (eventType) -> if eventType == "Quit"
			allClear!
			clearTempFiles!
		\onAppChange (settingName) -> switch settingName
			when "Theme"
				config.themeColor = App.themeColor\toARGB!
			when "Locale"
				config.locale = App.locale
				updateLocale!
				teal.clear true
			when "FullScreen", "Size", "Position"
				if App.platform in ["Linux", "Windows", "macOS"]
					switch settingName
						when "FullScreen" then config.fullScreen = App.fullScreen
						when "Position" then x: config.winX, y: config.winY = App.winPosition
						when "Size"
							:width, :height = App.winSize
							config.winWidth = width
							config.winHeight = height
		\onAppWS (eventType) -> if eventType == "Close"
			if HttpServer.wsConnectionCount == 0
				updateEntries!
		\slot "UpdateEntries", -> updateEntries!

setupEventHandlers!
clearTempFiles!

export stop = ->
	return false if isInEntry
	allClear!
	isInEntry = true
	currentEntry = nil
	true

export enterEntryAsync = (entry) ->
	isInEntry = false
	App.idled = false
	emit Profiler.EventName, "ClearLoader"
	currentEntry = entry
	{_, file, :workDir} = entry
	sleep!
	try
		workDir ??= Path\getPath file
		Content\insertSearchPath 1, workDir
		scriptPath = Path workDir, "Script"
		if Content\exist scriptPath
			Content\insertSearchPath 1, scriptPath
		result = require file
		result! if "function" == type result
		nil
	catch msg
		err = debug.traceback msg
		Log "Error", err
		allClear!
		import "UI.Control.Basic.ScrollArea"
		width: viewWidth, height: viewHeight = View.size
		width, height = viewWidth - 20, viewHeight - 20
		fontSize = math.floor 20 * App.devicePixelRatio
		Director.ui\addChild with root := AlignNode!
			:width, :height = App.bufferSize
			\css "width: #{width}; height: #{height}"
			\onAppChange (settingName) -> if settingName == "Size"
				:width, :height = App.bufferSize
				\css "width: #{width}; height: #{height}"
			\addChild with ScrollArea {
					:width
					:height
					paddingX: 0
					paddingY: 50
					viewWidth: height
					viewHeight: height
				}
				root\onAlignLayout (w, h) ->
					.position = Vec2 w / 2, h / 2
					w -= 20
					h -= 20
					.view.children.first.textWidth = w - fontSize
					\adjustSizeWithAlign "Auto", 10, Size w, h
				.view\addChild with label := Label "sarasa-mono-sc-regular", fontSize
					.alignment = "Left"
					.textWidth = width - fontSize
					.text = err
		err

enterDemoEntry = (entry) -> thread -> enterEntryAsync entry

reloadCurrentEntry = ->
	if currentEntry
		allClear!
		enterDemoEntry currentEntry

Director.clearColor = Color 0xff1a1a1a

isOSSLicenseExist = Content\exist "LICENSES"
ossLicenses = nil
ossLicenseOpen = false

extraOperations = ->
	zh = useChinese
	if isDesktop
		:themeColor = App
		:alwaysOnTop, :writablePath = config
		if changed, alwaysOnTop := Checkbox zh and "窗口置顶" or "Always On Top", alwaysOnTop
			App.alwaysOnTop = alwaysOnTop
			config.alwaysOnTop = alwaysOnTop
		SeparatorText zh and "工作目录" or "Workspace"
		PushTextWrapPos 400, ->
			TextColored themeColor, writablePath
		if Button zh and "改变目录" or "Set Folder"
			App\openFileDialog true, (path) ->
				setWorkspace path if path ~= ""
		SameLine!
		if Button zh and "使用默认" or "Use Default"
			setWorkspace Content.appPath
		Separator!
	if isOSSLicenseExist
		if Button zh and '开源协议' or 'OSS Licenses'
			unless ossLicenses
				ossLicenses = []
				licenseText = Content\load "LICENSES"
				if ossLicenseOpen := licenseText?
					licenseText = licenseText\gsub "\r\n", "\n"
					for license in GSplit licenseText, "\n--------\n", true
						if name, text := license\match "[%s\n]*([^\n]*)[\n]*(.*)"
							ossLicenses[] = [name, text]
			else
				ossLicenseOpen = true
		if ossLicenseOpen
			visualSize: {:width, :height}, :themeColor = App
			SetNextWindowPosCenter $SetCond(Appearing), Vec2 0.5, 0.5
			SetNextWindowSize Vec2(math.min(width * 0.8, 750), height * 0.8), $SetCond Appearing
			PushStyleVar "WindowPadding", Vec2(20, 10), -> ossLicenseOpen = Begin(
				zh and '开源协议' or 'OSS Licenses'
				ossLicenseOpen
				["NoSavedSettings",]
				-> for [firstLine, text] in *ossLicenses
					name, license = firstLine\match "(.+): (.+)"
					TextColored themeColor, name
					SameLine!
					TreeNode "#{license}###{name}", -> TextWrapped text
			)
	return unless App.debugging
	<- TreeNode zh and "开发操作" or "Development"
	OpenPopup "build" if Button zh and "脚本编译测试" or "Script Build Test"
	PushStyleVar "WindowPadding", Vec2(10, 10), -> BeginPopup "build", ->
		doCompile false if Selectable zh and "编译" or "Compile"
		Separator!
		doCompile true if Selectable zh and "压缩" or "Minify"
		Separator!
		doClean! if Selectable zh and "清理" or "Clean"
	if isInEntry
		if waitForWebStart
			BeginDisabled -> Button zh and "重载开发程序(Ctrl+Z)" or "Reload Dev Entry(Ctrl+Z)"
		elseif Button zh and "重载开发程序(Ctrl+Z)" or "Reload Dev Entry(Ctrl+Z)"
			reloadDevEntry!
	if changed, scaleContent := Checkbox string.format("%.1fx #{zh and '屏幕缩放' or 'Screen'}", screenScale), scaleContent
		View.scale = scaleContent and screenScale or 1
	if changed, engineDev := Checkbox zh and '引擎开发模式' or 'Engine Dev Mode', engineDev
		config.engineDev = engineDev
	if testingThread
		BeginDisabled -> Button zh and "开始自动测试" or "Test automatically"
	elseif Button zh and "开始自动测试" or "Test automatically"
		testingThread = thread ->
			close _ = <close>: ->
				allClear!
				testingThread = nil
				isInEntry = true
				currentEntry = nil
				print "Testing done!"
			for _, entry in ipairs allEntries
				allClear!
				print "Start #{entry[1]}"
				enterDemoEntry entry
				sleep 2
				print "Stop #{entry[1]}"

icon = Path "Script", "Dev", "icon_s.png"
iconTex = nil
thread -> iconTex = Texture2D icon if Cache\loadAsync icon

webStatus = nil
urlClicked = nil
descColor = Color 0xffa1a1a1

transparant = Color 0x0
windowFlags = $WindowFlag(
	NoTitleBar
	NoResize
	NoMove
	NoCollapse
	NoSavedSettings
	NoFocusOnAppearing
	NoBringToFrontOnFocus
)
statusFlags = $WindowFlag(
	NoTitleBar
	NoResize
	NoMove
	NoCollapse
	AlwaysAutoResize
	NoSavedSettings
)
displayWindowFlags = $WindowFlag(
	NoDecoration
	NoSavedSettings
	NoNav
	NoMove
	NoScrollWithMouse
	AlwaysAutoResize
)
initFooter = true
footerWindow = threadLoop ->
	zh = useChinese
	if HttpServer.wsConnectionCount > 0
		return
	if Keyboard\isKeyDown "Escape"
		allClear!
		App\shutdown!
	do
		ctrl = Keyboard\isKeyPressed "LCtrl"
		if ctrl and Keyboard\isKeyDown "Q"
			stop!
		if ctrl and Keyboard\isKeyDown "Z"
			reloadCurrentEntry!
		if ctrl and Keyboard\isKeyDown ","
			showStats = if showFooter then not showStats else true
			showFooter = true
			config.showFooter = showFooter
			config.showStats = showStats
		if ctrl and Keyboard\isKeyDown "."
			showConsole = if showFooter then not showConsole else true
			showFooter = true
			config.showFooter = showFooter
			config.showConsole = showConsole
		if ctrl and Keyboard\isKeyDown "/"
			showFooter = not showFooter
			config.showFooter = showFooter
		left = ctrl and Keyboard\isKeyDown "Left"
		right = ctrl and Keyboard\isKeyDown "Right"
		currentIndex = nil
		for i, entry in ipairs allEntries
			if currentEntry == entry
				currentIndex = i
		if left
			allClear!
			currentIndex ??= #allEntries + 1
			enterDemoEntry if currentIndex > 1
				allEntries[currentIndex - 1]
			else
				allEntries[#allEntries]
		if right
			allClear!
			currentIndex ??= 0
			enterDemoEntry if currentIndex < #allEntries
				allEntries[currentIndex + 1]
			else
				allEntries[1]
	return unless showEntry

	if isInEntry and
		not waitForWebStart and
		Keyboard\isKeyPressed("LCtrl") and
		Keyboard\isKeyDown "Z"
		reloadDevEntry!
	if initFooter
		initFooter = false
	
	:width, :height = App.visualSize
	if isInEntry or showFooter
		SetNextWindowSize Vec2 width, 50
		SetNextWindowPos Vec2 0, height - 50
		<- PushStyleVar "WindowPadding", Vec2 10, 0
		<- PushStyleVar "WindowRounding", 0
		<- Begin "Footer", windowFlags
		Separator!
		if iconTex
			if ImageButton "sideBtn", icon, Vec2 20, 20
				showStats = not showStats
				config.showStats = showStats
			SameLine!
			if Button ">_", Vec2 30, 30
				showConsole = not showConsole
				config.showConsole = showConsole
		if isInEntry and config.updateNotification
			SameLine!
			if ImGui.Button zh and "更新可用" or "Update"
				allClear!
				config.updateNotification = false
				enterDemoEntry [
					"SelfUpdater"
					Path Content.assetPath, "Script", "Tools", "SelfUpdater"
				]
		unless isInEntry
			SameLine!
			back = Button zh and "退出" or "Quit", Vec2 70, 30
			currentIndex = nil
			for i, entry in ipairs allEntries
				if currentEntry == entry
					currentIndex = i
			if currentIndex
				if currentIndex > 1
					SameLine!
					if Button "<<", Vec2 30, 30
						allClear!
						enterDemoEntry allEntries[currentIndex - 1]
				if currentIndex < #allEntries
					SameLine!
					if Button ">>", Vec2 30, 30
						allClear!
						enterDemoEntry allEntries[currentIndex + 1]
			SameLine!
			if Button zh and "刷新" or "Reload", Vec2 70, 30
				reloadCurrentEntry!
			if back
				allClear!
				isInEntry = true
				currentEntry = nil

	showWebIDE = isInEntry
	if config.updateNotification
		if width < 460
			showWebIDE = false
	else
		if width < 360
			showWebIDE = false
	if showWebIDE
		SetNextWindowBgAlpha 0.0
		SetNextWindowPos Vec2(width, height - 50), $SetCond(Always), Vec2 1, 0
		Begin "Web IDE", displayWindowFlags, ->
			if url := webStatus?.url
				if isDesktop and not config.fullScreen
					if urlClicked
						BeginDisabled -> Button url
					elseif Button url
						urlClicked = once -> sleep 5
						App\openURL "http://localhost:8866"
				else
					TextColored descColor, url
			else
				TextColored descColor, zh and '不可用' or 'not available'
			SameLine!
			TextDisabled '(?)'
			if IsItemHovered!
				BeginTooltip ->
					PushTextWrapPos 280, ->
						Text zh and '在本机或是本地局域网连接的其他设备上，使用浏览器访问这个地址来使用 Web IDE' or 'You can use the Web IDE by accessing this address in a browser on this machine or other devices connected to the local network'

	unless isInEntry
		SetNextWindowSize Vec2 50, 50
		SetNextWindowPos Vec2 width - 50, height - 50
		PushStyleColor "WindowBg", transparant, -> Begin "Show", displayWindowFlags, ->
			if width >= 370
				if changed, showFooter := Checkbox "##dev", showFooter
					config.showFooter = showFooter

	if isInEntry or showFooter
		if showStats
			<- PushStyleVar "WindowRounding", 0
			SetNextWindowPos Vec2(0, 0), $SetCond Always
			SetNextWindowSize Vec2 0, height - 50
			showStats = ShowStats showStats, statusFlags, extraOperations
			config.showStats = showStats
		if showConsole
			SetNextWindowPos Vec2(width - 425, height - 375), $SetCond FirstUseEver
			<- PushStyleVar "WindowRounding", 6
			ShowConsole!

const MaxWidth = 960

toolOpen = false
filterText = nil
anyEntryMatched = false
match = (name) ->
	res = not filterText or name\lower!\match filterText
	anyEntryMatched = true if res
	res

sep = -> SeparatorText ""
thinSep = -> PushStyleVar "SeparatorTextBorderSize", 1, sep

entryWindow = threadLoop ->
	if App.fpsLimited ~= config.fpsLimited
		config.fpsLimited = App.fpsLimited
	if App.targetFPS ~= config.targetFPS
		config.targetFPS = App.targetFPS
	if View.vsync ~= config.vsync
		config.vsync = View.vsync
	if Director.scheduler.fixedFPS ~= config.fixedFPS
		config.fixedFPS = Director.scheduler.fixedFPS
	if Director.profilerSending ~= config.webProfiler
		config.webProfiler = Director.profilerSending
	if urlClicked
		_, result = coroutine.resume urlClicked
		if result
			coroutine.close urlClicked
			urlClicked = nil
	return unless showEntry
	return unless isInEntry
	zh = useChinese
	if HttpServer.wsConnectionCount > 0
		:themeColor = App
		:width, :height = App.visualSize
		SetNextWindowBgAlpha 0.5
		SetNextWindowPos Vec2(width / 2, height / 2), $SetCond(Always), Vec2 0.5, 0.5
		Begin "Web IDE Connected", displayWindowFlags, ->
			Separator!
			TextColored themeColor, "#{zh and 'Web IDE 已连接 ……' or 'Web IDE connected ...'}"
			if iconTex
				Image icon, Vec2 24, 24
				SameLine!
			slogon = zh and 'Dora 启动！' or 'Dora Start!'
			TextColored descColor, slogon
			Separator!
		return

	:themeColor = App
	width: fullWidth, :height = App.visualSize
	width = math.min MaxWidth, fullWidth
	paddingX = math.max 10, fullWidth / 2 - width / 2 - 10
	maxColumns = math.max math.floor(width / 200), 1
	SetNextWindowPos Vec2.zero
	SetNextWindowBgAlpha 0
	SetNextWindowSize Vec2 fullWidth, 60
	do
		<- PushStyleVar "WindowPadding", Vec2 10, 0
		<- Begin "Dora Dev", windowFlags
		Dummy Vec2 fullWidth - 20, 0
		TextColored themeColor, "Dora SSR #{zh and '开发' or 'Dev'}"
		if fullWidth >= 400
			SameLine!
			Dummy Vec2 fullWidth - 400, 0
			SameLine!
			SetNextItemWidth zh and -95 or -140
			if InputText zh and '筛选' or 'Filter', filterBuf, ["AutoSelectAll",]
				config.filter = filterBuf.text
			SameLine!
			if Button zh and '下载' or 'Download'
				allClear!
				enterDemoEntry [
					"ResourceDownloader"
					Path Content.assetPath, "Script", "Tools", "ResourceDownloader"
				]
		Separator!
		Dummy Vec2 fullWidth - 20, 0

	anyEntryMatched = false
	SetNextWindowPos Vec2 0, 50
	SetNextWindowSize Vec2 fullWidth, height - 100
	do
		<- PushStyleColor "WindowBg", transparant
		<- PushStyleVar "WindowPadding", Vec2 paddingX, 10
		<- PushStyleVar "Alpha", 1
		<- Begin "Content", windowFlags
		const DemoViewWidth = 320
		filterText = filterBuf.text\match "[^%%%.%[]+"
		filterText = filterText\lower! if filterText
		if #gamesInDev > 0
			columns = math.max math.floor(width / DemoViewWidth), 1
			Columns columns, false
			realViewWidth = GetColumnWidth! - 50
			for game in *gamesInDev
				[gameName, fileName, examples, tests, bannerFile, bannerTex] = game
				if match gameName
					TextColored themeColor, zh and "项目：" or "Project:"
					SameLine!
					TextWrapped gameName
					if columns > 1
						if bannerFile
							width: texWidth, height: texHeight = bannerTex
							const displayWidth = realViewWidth
							texHeight = displayWidth * texHeight / texWidth
							texWidth = displayWidth
							Dummy Vec2.zero
							SameLine!
							Image bannerFile, Vec2 texWidth + 10, texHeight
						if Button "#{zh and "开始运行" or "Game Start"}###{fileName}", Vec2 -1, 40
							enterDemoEntry game
					else
						if bannerFile
							width: texWidth, height: texHeight = bannerTex
							displayWidth = (fullWidth / 2 - paddingX) * 2 - 35
							sizing = 0.8
							texHeight = displayWidth * sizing * texHeight / texWidth
							texWidth = displayWidth * sizing
							if texWidth > 500
								sizing = 0.6
								texHeight = displayWidth * sizing * texHeight / texWidth
								texWidth = displayWidth * sizing
							padding = displayWidth * (1 - sizing) / 2 - 10
							Dummy Vec2 padding, 0
							SameLine!
							Image bannerFile, Vec2 texWidth, texHeight
						if Button "#{zh and "开始运行" or "Game Start"}###{fileName}", Vec2 -1, 40
							enterDemoEntry game
					if #tests == 0 and #examples == 0
						thinSep!
					NextColumn!
				showSep = false
				if #examples > 0
					showExample = false
					showExample = for [name,] in *examples
						break true if match name
					if showExample
						showSep = true
						Columns 1, false
						TextColored themeColor, zh and "示例：" or "Example:"
						SameLine!
						opened = if filterText? then showExample else false
						game.exampleOpen ??= opened
						SetNextItemOpen game.exampleOpen
						TreeNode "#{gameName}##example-#{fileName}", ->
							<- PushStyleVar "ItemSpacing", Vec2 20, 10
							Columns maxColumns, false
							for example in *examples
								continue unless match example[1]
								PushID "#{gameName} #{example[1]} example", ->
									if Button example[1], Vec2 -1, 40
										enterDemoEntry example
									NextColumn!
								opened = true
						game.exampleOpen = opened
				if #tests > 0
					showTest = false
					showTest = for [name,] in *tests
						break true if match name
					if showTest
						showSep = true
						Columns 1, false
						TextColored themeColor, zh and "测试：" or "Test:"
						SameLine!
						opened = if filterText? then showTest else false
						game.testOpen ??= opened
						SetNextItemOpen game.testOpen
						TreeNode "#{gameName}##test-#{fileName}", ->
							<- PushStyleVar "ItemSpacing", Vec2 20, 10
							Columns maxColumns, false
							for test in *tests
								continue unless match test[1]
								PushID "#{gameName} #{test[1]} test", ->
									if Button test[1], Vec2 -1, 40
										enterDemoEntry test
									NextColumn!
								opened = true
						game.testOpen = opened
				if showSep
					Columns 1, false
					thinSep!
					Columns columns, false
		if #doraTools > 0
			showTool = false
			showTool = for [name,] in *doraTools
				break true if match name
			goto endEntry unless showTool
			Columns 1, false
			TextColored themeColor, "Dora SSR:"
			SameLine!
			Text zh and "开发支持" or "Development Support"
			Separator!
			if #doraTools > 0
				opened = if filterText? then showTool else false
				SetNextItemOpen toolOpen
				TreeNode zh and "引擎工具" or "Engine Tools", ->
					<- PushStyleVar "ItemSpacing", Vec2 20, 10
					Columns maxColumns, false
					for example in *doraTools
						continue unless match example[1]
						if Button example[1], Vec2 -1, 40
							enterDemoEntry example
						NextColumn!
					Columns 1, false
					opened = true
				toolOpen = opened
		::endEntry::
		unless anyEntryMatched
			SetNextWindowBgAlpha 0
			SetNextWindowPos Vec2(fullWidth / 2, height / 2), $SetCond(Always), Vec2 0.5, 0.5
			Begin "Entries Not Found", displayWindowFlags, ->
				Separator!
				TextColored themeColor, zh and "多萝：" or "Dora:"
				TextColored descColor, zh and '别担心，改变一些咒语，我们会找到新的冒险～' or 'Don\'t worry, more magic words and we\'ll find a new adventure!'
				Separator!
		Columns 1, false
		Dummy Vec2 100, 80
		ScrollWhenDraggingOnVoid!

webStatus = require "Script.Dev.WebServer"
