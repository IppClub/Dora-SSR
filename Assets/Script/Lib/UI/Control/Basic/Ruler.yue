--[[ Copyright (c) 2016-2025 Li Jin <dragon-fly@qq.com>

Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE. ]]

_ENV = Dora
import "UI.View.Control.Basic.Ruler"
import "Utils" as :Round

export default Class Ruler,
	__init: (args) =>
		{
			:y = 0
			:width
			:height
			:fontName = "sarasa-mono-sc-regular"
			:fontSize = 30
			:fixed = false
		} = args
		viewSize = View.size
		halfW = width / 2
		halfH = height / 2
		interval = 10
		indent = 100
		fontSize ??= 12
		fontSize = math.floor fontSize
		vsCache = {}
		@endPosY = y
		@isFixed = fixed ?? true

		labels = {}
		labelList = {}
		len = nil
		do
			posX = @intervalNode.anchor.x * width
			center = Round posX / 100
			len = Round (posX + halfW) / 100 - center
			len = 1 + math.max (center - Round (posX - halfW) / 100), len
			for i = center - len, center + len
				pos = i * 100
				label = with Label fontName, fontSize
					.text = string.format "%.0f", pos / 100 * indent
					.scaleX = 1 / @intervalNode.scaleX
					.position = Vec2 pos, halfH - 18 - fontSize
					.tag = tostring pos
				@intervalNode\addChild label
				labels[pos] = label
				table.insert labelList, label

		moveLabel = (label, pos) ->
			labels[tonumber label.tag] = nil
			labels[pos] = with label
				.text = string.format "%.0f", pos / 100 * indent
				.scaleX = 1 / @intervalNode.scaleX
				.position = Vec2 pos, halfH - 18 - fontSize
				.tag = tostring pos

		updateLabels = ->
			posX = @intervalNode.anchor.x * width
			center = math.floor posX / 100
			right = center + len
			left = center - len
			insertPos = 1
			for i = left, right
				pos = i * 100
				if labels[pos]
					break
				else
					label = table.remove labelList
					table.insert labelList, insertPos, label
					insertPos += 1
					moveLabel label, pos
			insertPos = #labelList
			for i = right, left, -1
				pos = i * 100
				if labels[pos]
					break
				else
					label = table.remove labelList, 1
					table.insert labelList, insertPos, label
					insertPos -= 1
					moveLabel label, pos

			scale = @intervalNode.scaleX
			current = Round @intervalNode.anchor.x * width / interval
			delta = 1 + math.ceil halfW / scale / interval
			max = current + delta
			min = current - delta
			count = 1
			vs = {}
			for i = min, max
				posX = i * interval
				v = vsCache[count]
				if v then v = Vec2 posX, halfH
				else
					v = Vec2 posX, halfH
					vsCache[count] = v
				vs[count] = v
				count += 1
				v = vsCache[count]
				if v then v = Vec2 posX, halfH - (i % 10 == 0 and fontSize + 6 or fontSize - 2)
				else
					v = Vec2 posX,halfH - (i % 10 == 0 and fontSize + 6 or fontSize - 2)
					vsCache[count] = v
				vs[count] = v
				count += 1
				v = vsCache[count]
				if v then v = Vec2 posX,halfH
				else
					v = Vec2 posX,halfH
					vsCache[count] = v
				vs[count] = v
				count += 1
			@intervalNode\set vs, Color 0xffffffff

		updateIntervalTextScale = (scale) ->
			@intervalNode\eachChild (child) ->
				child.scaleX = scale

		@makeScale = (scale) =>
			scale = math.min scale, 5
			@intervalNode.scaleX = scale
			-- unscale interval text --
			updateIntervalTextScale 1 / scale
			updateLabels!

		@makeScaleTo = (scale) =>
			with @intervalNode
				\perform ScaleX 0.5, @intervalNode.scaleX, scale, Ease.OutQuad
				-- manually update and unscale interval text --
				\schedule once -> cycle 0.5, -> updateIntervalTextScale 1 / .scaleX
			updateLabels!

		_value = 0
		_max = 0
		_min = 0

		switch App.platform
			when "macOS", "Windows", "Linux"
				@addChild with Node!
					.size = Size width, height
					.touchEnabled = true
					.swallowMouseWheel = true
					\slot "MouseWheel", (delta)->
						newVal = @getValue! + delta.y * indent / 10
						@setValue _min < _max and math.min(math.max(_min, newVal), _max) or newVal

		@setIndent = (_self, ind) ->
			indent = ind
			for i, label in pairs labels
				label.text = string.format "%.0f", ind * i / 100
		@getIndent = (_self) -> indent

		@lastValue = nil
		@setValue = (v) =>
			_value = v
			val = _min < _max and math.min(math.max(_value, _min), _max) or _value
			val = @isFixed and Round(val / (indent / 10)) * math.floor(indent / 10) or val
			val = 0 if val == -0
			if @lastValue ~= val
				@lastValue = val
				@emit "Changed", val
			posX = v * 10 * interval / indent
			@intervalNode.anchor = Vec2 posX / width, 0
			updateLabels!

		@getValue = (_self) -> _value
		@getPos = (_self) -> _value * 10 * interval / indent

		@setLimit = (_self, min, max) ->
			_max = max
			_min = min

		time = 0
		startPos = 0
		updateReset = (deltaTime) ->
			return if _min >= _max
			scale = @intervalNode.scaleX
			time = time + deltaTime
			t = time / 1
			t = t / 0.1 if scale < 1
			t = math.min 1, t
			yVal = nil
			if startPos < _min
				yVal = startPos + (_min - startPos) * Ease\func scale < 1 and Ease.Linear or Ease.OutElastic, t
			elseif startPos > _max
				yVal = startPos + (_max - startPos) * Ease\func scale < 1 and Ease.Linear or Ease.OutElastic, t
			@setValue ((yVal and yVal or 0) - _value) / scale + _value
			@unschedule! if t == 1.0

		isReseting = ->
			_min < _max and (_value > _max or _value < _min)

		startReset = ->
			startPos = _value
			time = 0
			@schedule updateReset

		_v = 0
		_s = 0
		updateSpeed = (deltaTime) ->
			return if _s == 0
			_v = _s / deltaTime
			_s = 0

		updatePos = (deltaTime) ->
			val = viewSize.height * 2
			a = _v > 0 and -val or val
			yR = _v > 0
			_v = _v + a * deltaTime
			if (_v < 0) == yR
				_v = 0
				a = 0
			ds = _v * deltaTime + a * (0.5 * deltaTime * deltaTime)
			newValue = _value - ds * indent / (interval * 10)
			@setValue (newValue - _value) / @intervalNode.scaleY + _value
			if _v == 0 or isReseting!
				if isReseting! then startReset!
				else @unschedule!

		@slot "TapFilter", (touch) ->
			touch.enabled = false unless touch.first

		@slot "TapBegan", ->
			_s = 0
			_v = 0
			@schedule updateSpeed

		@slot "TapMoved", (touch) ->
			deltaX = touch.delta.x
			v = _value - deltaX * indent / (interval * 10)
			padding = 0.5 * indent
			if _max > _min
				d = 1
				if v > _max
					d = (v - _max) * 3 / padding
				elseif v < _min
					d = (_min - v) * 3 / padding
				v = _value + (v - _value) / (d < 1 and 1 or d * d)
			@setValue (v - _value) / @intervalNode.scaleX + _value
			_s += deltaX

		@slot "TapEnded", ->
			if isReseting!
				startReset!
			elseif _v ~= 0
				@schedule updatePos

	value: property => @getValue!,
		(v) => @setValue v

	show: (default, min, max, ind, callback) =>
		@setLimit min, max
		@setIndent ind
		@slot("Changed")\set callback
		@lastValue = nil
		@setValue default
		@visible = true
		@perform Spawn(
			Y 0.5, @endPosY + 30, @endPosY, Ease.OutBack
			Opacity 0.3, @opacity, 1
		)

	hide: =>
		return unless @visible
		@slot "Changed", nil
		@unschedule!
		@perform Sequence(
			Spawn(
				Y 0.5, @y, @endPosY + 30, Ease.InBack
				Opacity 0.5, @opacity, 0
			),
			Hide!
		)
