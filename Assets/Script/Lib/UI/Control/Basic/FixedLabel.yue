--[[ Copyright (c) 2016-2025 Li Jin <dragon-fly@qq.com>

Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE. ]]

_ENV = Dora
import "utf-8" as utf8

Class Node,
	__init: (args) =>
		{
			:x = 0
			:y = 0
			:width = 0
			:height = 0
			:text = ""
			:fontName = "sarasa-mono-sc-regular"
			:fontSize = 25
			:textAlign = "Left"
			:color3 = Color 0xffffff
			:renderOrder = 0
		} = args
		@position = Vec2 x, y
		@size = Size width, height
		label = with Label fontName, fontSize
			.batched = false
			.alignment = textAlign
			.renderOrder = renderOrder
			.textWidth = width + 5
			switch textAlign
				when "Center"
					.position = Vec2(0.5, 0.5) * @size
				when "Left"
					.y = height / 2
					.anchor = Vec2 0, 0.5
				when "Right"
					.x = width
					.y = height / 2
					.anchor = Vec2 1, 0.5
		@addChild label
		@_label = label
		@text = text

	text: property (=> @_text),
		(value) =>
			@_text = value
			@_label.text = value
			:width, :height = @
			charCount = @_label.characterCount
			if charCount > 0
				char = @_label\getCharacter 1
				return unless char
				left = char.x - char.width / 2
				top = char.y + char.height / 2
				for i = 2, charCount
					char = @_label\getCharacter i
					continue unless (char and char.visible)
					right = char.x + char.width / 2
					bottom = char.y - char.height / 2
					if (right - left) > width or (top - bottom) > height
						displayText = utf8.sub value, 1, i - 4
						displayText ..= "..."
						@_label.text = displayText
						break
