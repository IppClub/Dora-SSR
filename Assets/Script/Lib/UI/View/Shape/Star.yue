--[[ Copyright (c) 2025 Li Jin, dragon-fly@qq.com

Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE. ]]

_ENV = Dora

StarVertices = (radius, line=true) ->
	a = math.rad 36
	c = math.rad 72
	f = math.sin(a) * math.tan(c) + math.cos a
	R = radius
	r = R / f
	return for i = 9 + (line and 1 or 0), 0, -1
		angle = i * a
		cr = i % 2 == 1 and r or R
		Vec2 cr * math.sin(angle), cr * math.cos angle

export default (args) ->
	with Node!
		.position = Vec2 args.x or 0, args.y or 0
		if args.fillColor
			\addChild with DrawNode!
				\drawPolygon StarVertices(args.size, false), Color args.fillColor
				.renderOrder = args.fillOrder if args.fillOrder
		if args.borderColor
			\addChild with Line StarVertices(args.size), Color args.borderColor
				.renderOrder = args.lineOrder if args.lineOrder
