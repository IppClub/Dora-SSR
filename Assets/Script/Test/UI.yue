-- @preview-file on
_ENV = Dora
import "UI.Control.Basic.Button"
import "UI.View.Shape.LineRect"
import "UI.Control.Basic.ScrollArea"

Panel = (width, height, viewWidth, viewHeight) ->
	with ScrollArea {
			:width
			:height
			paddingX: 50
			paddingY: 50
			:viewWidth
			:viewHeight
		}
		for i = 1, 50
			.view\addChild with Button {
					text: "点击\n按钮#{i}"
					width: 60
					height: 60
					fontName: "sarasa-mono-sc-regular"
					fontSize: 16
				}
				\onTapped -> print "clicked #{i}"
		.view\alignItems Size viewWidth, height
		.updateSize = (w, h) =>
			@position = Vec2 w / 2, h / 2
			@adjustSizeWithAlign "Auto", 10, Size(w, h), Size width, h
			@border\removeFromParent! if @border
			@border = LineRect x: -w / 2, y: -h / 2, width: w, height: h, color: 0xffffffff
			\addChild @border

Director.ui\addChild with AlignNode true
	\css "justify-content: space-between; flex-direction: row"
	\addChild with AlignNode!
		\css "width: 30%; height: 100%; padding: 10"
		\addChild with AlignNode!
			\css "width: 100%; height: 100%"
			panel = Panel 500, 1000, 1000, 1000
			\addChild panel
			\onAlignLayout (w, h) -> panel\updateSize w, h
	\addChild with AlignNode!
		\css "width: 40%; height: 100%; padding: 10; justify-content: center"
		\addChild with AlignNode!
			\css "width: 100%; height: 50%"
			panel = Panel 600, 1000, 1000, 1000
			\addChild panel
			\onAlignLayout (w, h) -> panel\updateSize w, h
	\addChild with AlignNode!
		\css "width: 30%; height: 100%; padding: 10; flex-direction: column-reverse"
		\addChild with AlignNode!
			\css "width: 100%; height: 40%"
			panel = Panel 600, 1000, 1000, 1000
			\addChild panel
			\onAlignLayout (w, h) -> panel\updateSize w, h
