_ENV = Dora

node = with Node!
	.order = 2
	\addChild with Model "Model/xiaoli.model"
		.y = -80
		.fliped = true
		.look = "happy"
		\play "walk", true
		\runAction Sequence(
			X 2, -150, 250
			Event "Turn"
			X 2, 250, -150
			Event "Turn"
		), true
		\slot "Turn", -> .fliped = not .fliped

renderTarget = with RenderTarget 300, 400
	\renderWithClear Color 0xff8a8a8a

with surface := Sprite renderTarget.texture
	.order = 1
	.z = 300
	.angleY = 25
	\addChild Line [
		Vec2.zero
		Vec2 300, 0
		Vec2 300, 400
		Vec2 0, 400
		Vec2.zero
	], App.themeColor
	\schedule ->
		node.y = 200
		renderTarget\renderWithClear node, Color 0xff8a8a8a
		node.y = 0

-- example codes ends here, some test ui below --

import "ImGuiEnum" as :$WindowFlag
windowFlags = $WindowFlag(
	NoDecoration
	AlwaysAutoResize
	NoSavedSettings
	NoFocusOnAppearing
	NoNav
	NoMove
)
threadLoop ->
	const :width = App.visualSize
	ImGui.SetNextWindowBgAlpha 0.35
	ImGui.SetNextWindowPos Vec2(width - 10, 10), "Always", Vec2 1, 0
	ImGui.SetNextWindowSize Vec2(240, 0), "FirstUseEver"
	ImGui.Begin "Render Target", windowFlags, ->
		ImGui.Text "Render Target (Yuescript)"
		ImGui.Separator!
		ImGui.TextWrapped "Use render target node as a mirror!"
