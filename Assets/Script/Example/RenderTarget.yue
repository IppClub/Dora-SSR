_ENV = Dorothy builtin.ImGui

node = with Node!
	\addChild with Model "Model/xiaoli.model"
		.y = -80
		.fliped = true
		.look = "happy"
		\play "walk", true
		\runAction Sequence(
			X 2, -150, 250
			Event "Turn"
			X 2, 250, -150
			Event "Turn"
		)
		\slot "ActionEnd", (action)-> \runAction action
		\slot "Turn", -> .fliped = not .fliped

renderTarget = with RenderTarget 300, 400
	\renderWithClear Color 0xff8a8a8a

surface = with Sprite renderTarget.texture
	.z = 300
	.angleY = 25
	\addChild Line {
		Vec2.zero
		Vec2 300, 0
		Vec2 300, 400
		Vec2 0, 400
		Vec2.zero
	}, Color 0xff00ffff
	\schedule ->
		node.y = 200
		renderTarget\renderWithClear node, Color 0xff8a8a8a
		node.y = 0

Director.entry\addChild with Node!
	\addChild surface
	\addChild node

-- example codes ends here, some test ui below --

Director.entry\addChild with Node!
	windowFlags =
		* "NoDecoration"
		* "AlwaysAutoResize"
		* "NoSavedSettings"
		* "NoFocusOnAppearing"
		* "NoNav"
		* "NoMove"
	\schedule ->
		const :width = App.visualSize
		SetNextWindowBgAlpha 0.35
		SetNextWindowPos Vec2(width - 10, 10), "Always", Vec2 1, 0
		SetNextWindowSize Vec2(240, 0), "FirstUseEver"
		Begin "Render Target", windowFlags, ->
			Text "Render Target"
			Separator!
			TextWrapped "Use render target node as a mirror!"
