_ENV = Dorothy builtin.ImGui

StarVertices = (radius, line = false)->
	a = math.rad 36
	c = math.rad 72
	f = math.sin(a) * math.tan(c) + math.cos a
	R = radius
	r = R / f
	return for i = 9, line and -1 or 0, -1
		angle = i * a
		cr = i % 2 == 1 and r or R
		Vec2 cr * math.sin(angle), cr * math.cos angle

-- example A

maskA = with DrawNode!
	\drawPolygon StarVertices 160

targetA = with Model "Model/xiaoli.model"
	.look = "happy"
	.fliped = true
	\play "walk", true
	\runAction Sequence(
		X 1.5, -200, 200
		Event "Turn"
		X 1.5, 200, -200
		Event "Turn"
	)
	\slot "ActionEnd", (action)-> \runAction action
	\slot "Turn", -> .fliped = not .fliped

clipNodeA = with ClipNode maskA
	\addChild targetA
	.inverted = true
frame = with Line StarVertices(160, true), Color 0xff00ffff
	.visible = false
exampleA = with Node!
	\addChild clipNodeA
	\addChild frame
	\addTo Director.entry
	.visible = false

-- example B

maskB = with Model "Model/xiaoli.model"
	.look = "happy"
	.fliped = true
	\play "walk", true

targetB = with DrawNode!
	\drawPolygon StarVertices 160
	\runAction Sequence(
		X 1.5, -200, 200
		X 1.5, 200, -200
	)
	\slot "ActionEnd", (action)-> \runAction action

clipNodeB = with ClipNode maskB
	\addChild targetB
	.inverted = true
	.alphaThreshold = 0.3
exampleB = with Node!
	\addChild clipNodeB
	\addTo Director.entry

-- example codes ends here, some test ui below --

inverted = true
withAlphaThreshold = true
Director.entry\addChild with Node!
	windowFlags =
		* "NoDecoration"
		* "AlwaysAutoResize"
		* "NoSavedSettings"
		* "NoFocusOnAppearing"
		* "NoNav"
		* "NoMove"
	\schedule ->
		const :width = App.visualSize
		SetNextWindowPos Vec2(width - 10, 10), "Always", Vec2 1, 0
		SetNextWindowSize Vec2(240, 0), "FirstUseEver"
		Begin "Clip Node", windowFlags, ->
			Text "Clip Node"
			Separator!
			TextWrapped "Render children nodes with mask!"
			if changed, inverted = Checkbox "Inverted", inverted
				clipNodeA.inverted = inverted
				clipNodeB.inverted = inverted
				frame.visible = not inverted
			if changed, withAlphaThreshold = Checkbox "With alphaThreshold", withAlphaThreshold
				exampleB.visible = withAlphaThreshold
				exampleA.visible = not withAlphaThreshold
