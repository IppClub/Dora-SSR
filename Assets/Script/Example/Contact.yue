_ENV = Dorothy!

gravity = Vec2 0, -10

world = with PhysicsWorld!
	\setShouldContact 0, 0, true
	.showDebug = true

label = with Label "sarasa-mono-sc-regular", 30
	\addTo world

terrainDef = with BodyDef!
	count = 50
	radius = 300
	vertices = for i = 1, count + 1
		angle = 2 * math.pi * i / count
		Vec2 radius * math.cos(angle), radius * math.sin(angle)
	\attachChain vertices, 0.4, 0
	\attachDisk Vec2(0, -270), 30, 1, 0, 1.0
	\attachPolygon Vec2(0, 80), 120, 30, 0, 1, 0, 1.0

terrain = with Body terrainDef, world
	\addTo world

drawNode = with Line [
		Vec2 -20, 0
		Vec2 20, 0
		Vec2.zero
		Vec2 0, -20
		Vec2 0, 20
	], App.themeColor
	\addTo world

diskDef = with BodyDef!
	.type = "Dynamic"
	.linearAcceleration = gravity
	\attachDisk 20, 5, 0.8, 1

disk = with Body diskDef, world, Vec2 100, 200
	\addTo world
	.angularRate = -1800
	.receivingContact = true
	\slot "ContactStart", (_, point)->
		drawNode.position = point
		label.text = string.format "Contact: [%.0f,%.0f]", point.x, point.y

-- example codes ends here, some test ui below --

windowFlags =
	* "NoDecoration"
	* "AlwaysAutoResize"
	* "NoSavedSettings"
	* "NoFocusOnAppearing"
	* "NoNav"
	* "NoMove"
:receivingContact = disk
threadLoop ->
	const :width = App.visualSize
	ImGui.SetNextWindowBgAlpha 0.35
	ImGui.SetNextWindowPos Vec2(width - 10, 10), "Always", Vec2 1, 0
	ImGui.SetNextWindowSize Vec2(240, 0), "FirstUseEver"
	ImGui.Begin "Contact", windowFlags, ->
		ImGui.Text "Contact"
		ImGui.Separator!
		ImGui.TextWrapped "Receive events when physics bodies contact."
		if changed, receivingContact := ImGui.Checkbox "Receiving Contact", receivingContact
			disk.receivingContact = receivingContact
			label.text = ""
