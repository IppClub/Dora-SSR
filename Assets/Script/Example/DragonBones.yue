_ENV = Dora!

boneStr = "DragonBones/NewDragon"

animations = DragonBone\getAnimations boneStr
looks = DragonBone\getLooks boneStr

p animations, looks

bone = with DragonBone boneStr
	.look = looks[1]
	\play animations[1], true
	\slot "AnimationEnd", (name) -> print "#{name} end!"
	.y = -200
	.touchEnabled = true
	\slot "TapBegan", (touch) ->
		:x, :y = touch.location
		if name := \containsPoint x, y
			\addChild with Label "sarasa-mono-sc-regular", 30
				.text = name
				.color = App.themeColor
				\perform Sequence Spawn(
					Scale 1, 0, 2, Ease.OutQuad
					Sequence(
						Delay 0.5
						Opacity 0.5, 1, 0
					)
				), Event "Stop"
				.position = Vec2 x, y
				.order = 100
				\slot "Stop", -> \removeFromParent!

-- example codes ends here, some test ui below --

windowFlags =
	* "NoDecoration"
	* "AlwaysAutoResize"
	* "NoSavedSettings"
	* "NoFocusOnAppearing"
	* "NoNav"
	* "NoMove"
:showDebug = bone
threadLoop ->
	const :width = App.visualSize
	ImGui.SetNextWindowBgAlpha 0.35
	ImGui.SetNextWindowPos Vec2(width - 10, 10), "Always", Vec2 1, 0
	ImGui.SetNextWindowSize Vec2(240, 0), "FirstUseEver"
	ImGui.Begin "DragonBones", windowFlags, ->
		ImGui.Text "DragonBones (Yuescript)"
		ImGui.Separator!
		ImGui.TextWrapped "Basic usage to create dragonBones! Tap it for a hit test."
		if changed, showDebug := ImGui.Checkbox "BoundingBox", showDebug
			bone.showDebug = showDebug
