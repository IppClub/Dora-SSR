_ENV = Dora

modelFile = "Model/xiaoli.model"

model = with Model modelFile
	.recovery = 0.2
	.look = "happy"
	\play "walk", true
	\onAnimationEnd (name) -> print name, "end"

-- example codes ends here, some test ui below --

looks = Model\getLooks modelFile
looks[] = "" if #looks == 0
animations = Model\getAnimations modelFile
animations[] = "" if #animations == 0
currentLook = #looks
currentAnim = #animations
loop = true
import "ImGuiEnum" as :$WindowFlag
windowFlags = $WindowFlag NoResize, NoSavedSettings
threadLoop ->
	const :width = App.visualSize
	ImGui.SetNextWindowPos Vec2(width-250,10), "FirstUseEver"
	ImGui.SetNextWindowSize Vec2(240,325), "FirstUseEver"
	ImGui.Begin "Model", windowFlags, ->
		ImGui.Text "Model (Yuescript)"
		if changed, currentLook := ImGui.Combo "Look", currentLook, looks
			model.look = looks[currentLook]
		if changed, currentAnim := ImGui.Combo "Anim", currentAnim, animations
			model\play animations[currentAnim], loop
		if changed, loop := ImGui.Checkbox "Loop", loop
			model\play animations[currentAnim], loop
		ImGui.SameLine!
		if changed, model.reversed := ImGui.Checkbox "Reversed", model.reversed
			model\play animations[currentAnim], loop
		ImGui.PushItemWidth -70, ->
			_, model.speed = ImGui.DragFloat "Speed", model.speed, 0.01, 0, 10, "%.2f"
			_, model.recovery = ImGui.DragFloat "Recovery", model.recovery, 0.01, 0, 10, "%.2f"
		scale = model.scaleX
		_, scale = ImGui.DragFloat "Scale", scale, 0.01, 0.5, 2, "%.2f"
		model.scaleX, model.scaleY = scale, scale
		if ImGui.Button "Play", Vec2 140, 30
			model\play animations[currentAnim], loop
