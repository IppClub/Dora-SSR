_ENV = Dora

spineStr = "Spine/dragon-ess"

animations = Spine\getAnimations spineStr
looks = Spine\getLooks spineStr

p animations, looks

spine = with Spine spineStr
	.look = looks[1]
	\play animations[1], true
	\onAnimationEnd (name) -> print "#{name} end!"
	\onTapBegan (touch) ->
		:x, :y = touch.location
		if name := \containsPoint x, y
			\addChild with Label "sarasa-mono-sc-regular", 30
				.text = name
				.color = App.themeColor
				.position = Vec2 x, y
				\perform Sequence Spawn(
					Scale 1, 0, 2, Ease.OutQuad
					Sequence(
						Delay 0.5
						Opacity 0.5, 1, 0
					)
				), Event "Stop"
				\slot "Stop", -> \removeFromParent!

-- example codes ends here, some test ui below --

import "ImGuiEnum" as :$WindowFlag
windowFlags = $WindowFlag(
	NoDecoration
	AlwaysAutoResize
	NoSavedSettings
	NoFocusOnAppearing
	NoNav
	NoMove
)
:showDebug = spine
threadLoop ->
	const :width = App.visualSize
	ImGui.SetNextWindowBgAlpha 0.35
	ImGui.SetNextWindowPos Vec2(width - 10, 10), "Always", Vec2 1, 0
	ImGui.SetNextWindowSize Vec2(240, 0), "FirstUseEver"
	ImGui.Begin "Spine", windowFlags, ->
		ImGui.Text "Spine (Yuescript)"
		ImGui.Separator!
		ImGui.TextWrapped "Basic usage to create spine! Tap it for a hit test."
		if changed, showDebug := ImGui.Checkbox "BoundingBox", showDebug
			spine.showDebug = showDebug
