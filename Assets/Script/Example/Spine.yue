_ENV = Dorothy builtin.ImGui

spineStr = "Spine/dragon-ess"

animations = Spine\getAnimations spineStr
looks = Spine\getLooks spineStr

p animations, looks

spine = with Spine spineStr
	.look = looks[1]
	\play animations[1], true
	\slot "AnimationEnd", (name)-> print "#{name} end!"
	.touchEnabled = true
	\slot "TapBegan", (touch)->
		:x, :y = touch.location
		if name = \containsPoint x, y
			\addChild with Label "sarasa-mono-sc-regular", 30
				.text = name
				.color = Color 0xff00ffff
				\perform Sequence(Spawn(
					Scale 1, 0, 2, Ease.OutQuad
					Sequence(
						Delay 0.5
						Opacity 0.5, 1, 0
					)
				), Event "Stop")
				.position = Vec2 x, y
				\slot "Stop", -> \removeFromParent!

Director.entry\addChild spine

-- example codes ends here, some test ui below --

Director.entry\addChild with Node!
	windowFlags =
		* "NoDecoration"
		* "AlwaysAutoResize"
		* "NoSavedSettings"
		* "NoFocusOnAppearing"
		* "NoNav"
		* "NoMove"
	:showDebug = spine
	\schedule ->
		const :width = App.visualSize
		SetNextWindowBgAlpha 0.35
		SetNextWindowPos Vec2(width - 10, 10), "Always", Vec2 1, 0
		SetNextWindowSize Vec2(240, 0), "FirstUseEver"
		Begin "Spine", windowFlags, ->
			Text "Spine"
			Separator!
			TextWrapped "Basic usage to create spine! Tap it for a hit test."
			if changed, showDebug = Checkbox "BoundingBox", showDebug
				spine.showDebug = showDebug
