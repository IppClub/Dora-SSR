_ENV = Dorothy!

spineStr = "Spine/dragon-ess"

animations = Spine\getAnimations spineStr
looks = Spine\getLooks spineStr

p animations, looks

spine = with Spine spineStr
	.look = looks[1]
	\play animations[1], true
	\slot "AnimationEnd", (name)-> print "#{name} end!"
	.touchEnabled = true
	\slot "TapBegan", (touch)->
		:x, :y = touch.location
		if name := \containsPoint x, y
			\addChild with Label "sarasa-mono-sc-regular", 30
				.text = name
				.color = App.themeColor
				\perform Sequence Spawn(
					Scale 1, 0, 2, Ease.OutQuad
					Sequence(
						Delay 0.5
						Opacity 0.5, 1, 0
					)
				), Event "Stop"
				.position = Vec2 x, y
				\slot "Stop", -> \removeFromParent!

-- example codes ends here, some test ui below --

windowFlags =
	* "NoDecoration"
	* "AlwaysAutoResize"
	* "NoSavedSettings"
	* "NoFocusOnAppearing"
	* "NoNav"
	* "NoMove"
:showDebug = spine
threadLoop ->
	const :width = App.visualSize
	ImGui.SetNextWindowBgAlpha 0.35
	ImGui.SetNextWindowPos Vec2(width - 10, 10), "Always", Vec2 1, 0
	ImGui.SetNextWindowSize Vec2(240, 0), "FirstUseEver"
	ImGui.Begin "Spine", windowFlags, ->
		ImGui.Text "Spine"
		ImGui.Separator!
		ImGui.TextWrapped "Basic usage to create spine! Tap it for a hit test."
		if changed, showDebug := ImGui.Checkbox "BoundingBox", showDebug
			spine.showDebug = showDebug
