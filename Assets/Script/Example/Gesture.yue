_ENV = Dora!

texture = nvg.GetDoraSSR!
sprite = Sprite texture
length = Vec2(View.size).length
:width, :height = sprite
size = Vec2(width, height).length
scaledSize = size

with Node!
	\addChild sprite
	.touchEnabled = true
	\slot "Gesture", (center, _numTouches, delta, angle) ->
		-- center: center of all touch positions
		-- delta: changed motion ratio (compare to screen size) along the x-axes and y-axes
		-- angle: rotated angle along the touches` center
		sprite.position = center
		sprite.angle += angle
		scaledSize += delta * length
		sprite.scaleX = scaledSize / size
		sprite.scaleY = scaledSize / size

-- example codes ends here, some test ui below --

windowFlags =
	* "NoDecoration"
	* "AlwaysAutoResize"
	* "NoSavedSettings"
	* "NoFocusOnAppearing"
	* "NoNav"
	* "NoMove"
threadLoop ->
	const :width = App.visualSize
	ImGui.SetNextWindowBgAlpha 0.35
	ImGui.SetNextWindowPos Vec2(width - 10, 10), "Always", Vec2 1,0
	ImGui.SetNextWindowSize Vec2(240, 0), "FirstUseEver"
	ImGui.Begin "Gesture", windowFlags, ->
		ImGui.Text "Gesture"
		ImGui.Separator!
		ImGui.TextWrapped "Interact with multi-touches!"
