_ENV = Dora

with Node!
	\addChild with Model "Model/xiaoli.model"
		.look = "happy"
		\play "idle", true

	\addChild with Sprite "Image/logo.png"
		.scaleX = 0.4
		.scaleY = 0.4
		.position = Vec2 200, -100
		.angleY = 45
		.z = -300

	\schedule once -> with Director.currentCamera
		cycle 1.5, (dt) -> .position = Vec2 200 * Ease\func(Ease.InOutQuad, dt), 0
		cycle 0.1, (dt) -> .rotation = 25 * Ease\func Ease.OutSine, dt
		cycle 0.2, (dt) -> .rotation = 25 - 50 * Ease\func Ease.InOutQuad, dt
		cycle 0.1, (dt) -> .rotation = -25 + 25 * Ease\func Ease.OutSine, dt
		cycle 1.5, (dt) -> .position = Vec2 200 * Ease\func(Ease.InOutQuad, 1 - dt), 0
		zoom = .zoom
		cycle 2.5, (dt) -> .zoom = zoom + Ease\func Ease.InOutQuad, dt

-- example codes ends here, some test ui below --

import "ImGuiEnum" as {:$WindowFlag}
windowFlags = $WindowFlag(
	NoDecoration
	AlwaysAutoResize
	NoSavedSettings
	NoFocusOnAppearing
	NoNav
	NoMove
)
threadLoop ->
	const :width = App.visualSize
	ImGui.SetNextWindowPos Vec2(width - 10, 10), "Always", Vec2 1, 0
	ImGui.SetNextWindowSize Vec2(240, 0), "FirstUseEver"
	ImGui.Begin "Camera", windowFlags, ->
		ImGui.Text "Camera (Yuescript)"
		ImGui.Separator!
		ImGui.TextWrapped "View camera motions, use 3D camera as default!"
