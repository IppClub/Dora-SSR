_ENV = Dorothy builtin.ImGui

sceneGroup = Group {"scene"}
positionGroup = Group {"position"}

with Observer "Add", {"scene"}
	\watch (scene)=>
		Director.entry\addChild with scene
			.touchEnabled = true
			\slot "TapEnded", (touch)->
				:location = touch
				positionGroup\each (entity)->
					entity.target = location

with Observer "Add", {"sprite"}
	\watch (sprite)=>
		sceneGroup\each (e)->
			e.scene\addChild with sprite
				\runAction Scale 0.5, 0, 0.5, Ease.OutBack

with Observer "Remove", {"sprite"}
	\watch =>
		sceneGroup\each (e)->
			e.scene\removeChild @oldValues.sprite

with Observer "Remove", {"target"}
	\watch =>
		print "remove target from entity #{@index}"

with Group {"position", "direction", "speed", "target"}
	\watch (position, direction, speed, target)=>
		return if target == position
		dir = target - position
		dir = dir\normalize!
		angle = math.deg math.atan dir.x, dir.y
		newPos = position + dir * speed
		newPos = newPos\clamp position, target
		@position = newPos
		@direction = angle
		@target = nil if newPos == target

with Observer "AddOrChange", {"position", "direction", "sprite"}
	\watch (position, direction, sprite)=>
		sprite.position = position
		lastDirection = @oldValues.direction or sprite.angle
		if math.abs(direction - lastDirection) > 1
			sprite\runAction Roll 0.3, lastDirection, direction

Entity
	sprite: Sprite "Image/logo.png"
	position: Vec2.zero
	direction: 45.0
	speed: 4.0

Entity
	sprite: Sprite "Image/logo.png"
	position: Vec2 -100, 200
	direction: 90.0
	speed: 10.0

Entity
	scene: Node!

-- example codes ends here, some test ui below --

with Observer "Add", {"scene"}
	windowFlags =
		* "NoDecoration"
		* "AlwaysAutoResize"
		* "NoSavedSettings"
		* "NoFocusOnAppearing"
		* "NoNav"
		* "NoMove"
	\watch (entity)-> entity.scene\schedule ->
		const :width = App.visualSize
		SetNextWindowBgAlpha 0.35
		SetNextWindowPos Vec2(width - 10, 10), "Always", Vec2 1, 0
		SetNextWindowSize Vec2(240, 0), "FirstUseEver"
		Begin "ECS System", windowFlags, ->
			Text "ECS System"
			Separator!
			TextWrapped "Tap any place to move entities."
			if Button "Create Random Entity"
				Entity
					sprite: Sprite "Image/logo.png"
					position: Vec2 6 * math.random(1, 100), 6 * math.random(1, 100)
					direction: 1.0 * math.random 0, 360
					speed: 1.0 * math.random 1, 20
			if Button "Destroy An Entity"
				Group({"sprite", "position"})\each (entity)->
					entity.position = nil
					with entity.sprite
						\runAction Sequence Scale(0.5, 0.5, 0, Ease.InBack), Event("Destroy")
						\slot "Destroy", -> entity\destroy!
					true
