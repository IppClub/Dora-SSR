_ENV = Dora

sceneGroup = Group ["scene",]
positionGroup = Group ["position",]

with Observer "Add", ["scene",]
	\watch (scene): false => with scene
		\onTapEnded (touch) ->
			:location = touch
			positionGroup\each (entity) ->
				entity.target = location

with Observer "Add", ["image",]
	\watch (image): false => sceneGroup\each (e) ->
		with @sprite := Sprite image
			\addTo e.scene
			\runAction Scale 0.5, 0, 0.5, Ease.OutBack
		true

with Observer "Remove", ["sprite",]
	\watch => @oldValues.sprite\removeFromParent!

with Observer "Remove", ["target",]
	\watch => print "remove target from entity #{@index}"

with Group ["position", "direction", "speed", "target"]
	\watch (position, direction, speed, target): false =>
		return if target == position
		dir = target - position
		dir = dir\normalize!
		angle = math.deg math.atan dir.x, dir.y
		newPos = position + dir * speed
		newPos = newPos\clamp position, target
		@position = newPos
		@direction = angle
		@target = nil if newPos == target

with Observer "AddOrChange", ["position", "direction", "sprite"]
	\watch (position, direction, sprite): false =>
		sprite.position = position
		lastDirection = @oldValues.direction or sprite.angle
		if math.abs(direction - lastDirection) > 1
			sprite\runAction Roll 0.3, lastDirection, direction

Entity
	scene: Node!

Entity
	image: "Image/logo.png"
	position: Vec2.zero
	direction: 45.0
	speed: 4.0

Entity
	image: "Image/logo.png"
	position: Vec2 -100, 200
	direction: 90.0
	speed: 10.0

-- example codes ends here, some test ui below --

import "ImGuiEnum" as :$WindowFlag
windowFlags = $WindowFlag(
	NoDecoration
	AlwaysAutoResize
	NoSavedSettings
	NoFocusOnAppearing
	NoNav
	NoMove
)
threadLoop ->
	const :width = App.visualSize
	ImGui.SetNextWindowBgAlpha 0.35
	ImGui.SetNextWindowPos Vec2(width - 10, 10), "Always", Vec2 1, 0
	ImGui.SetNextWindowSize Vec2(240, 0), "FirstUseEver"
	ImGui.Begin "ECS System", windowFlags, ->
		ImGui.Text "ECS System (Yuescript)"
		ImGui.Separator!
		ImGui.TextWrapped "Tap any place to move entities."
		if ImGui.Button "Create Random Entity"
			Entity
				image: "Image/logo.png"
				position: Vec2 6 * math.random(1, 100), 6 * math.random 1, 100
				direction: 1.0 * math.random 0, 360
				speed: 1.0 * math.random 1, 20
		if ImGui.Button "Destroy An Entity"
			Group(["sprite", "position"])\each (entity) ->
				entity.position = nil
				with entity.sprite
					\runAction Sequence Scale(0.5, 0.5, 0, Ease.InBack), Event "Destroy"
					\slot "Destroy", -> entity\destroy!
				true
