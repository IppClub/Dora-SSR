_ENV = Dorothy!

const gravity = Vec2 0, -10

const groupZero = 0
const groupOne = 1
const groupTwo = 2

terrainDef = with BodyDef!
	.type = "Static"
	\attachPolygon 800, 10, 1, 0.8, 0.2

polygonDef = with BodyDef!
	.type = "Dynamic"
	.linearAcceleration = gravity
	\attachPolygon {
		Vec2 60, 0
		Vec2 30, -30
		Vec2 -30, -30
		Vec2 -60, 0
		Vec2 -30, 30
		Vec2 30, 30
	}, 1, 0.4, 0.4

diskDef = with BodyDef!
	.type = "Dynamic"
	.linearAcceleration = gravity
	\attachDisk 60, 1, 0.4, 0.4

world = with PhysicsWorld!
	.y = -200
	\setShouldContact groupZero, groupOne, false
	\setShouldContact groupZero, groupTwo, true
	\setShouldContact groupOne, groupTwo, true
	.showDebug = true

Director.entry\addChild with world
	\addChild with Body terrainDef, world, Vec2.zero
		.group = groupTwo

	\addChild with Body polygonDef, world, Vec2(0, 500), 15
		.group = groupOne

	\addChild with Body diskDef, world, Vec2(50, 800)
		.group = groupZero
		.angularRate = 90

-- example codes ends here, some test ui below --

_ENV = Dorothy builtin.ImGui

Director.entry\addChild with Node!
	windowFlags =
		* "NoDecoration"
		* "AlwaysAutoResize"
		* "NoSavedSettings"
		* "NoFocusOnAppearing"
		* "NoNav"
		* "NoMove"
	\schedule ->
		const :width = App.visualSize
		SetNextWindowBgAlpha 0.35
		SetNextWindowPos Vec2(width - 10, 10), "Always", Vec2 1, 0
		SetNextWindowSize Vec2(240, 0), "FirstUseEver"
		Begin "Body", windowFlags, ->
			Text "Body"
			Separator!
			TextWrapped "Basic usage to create physics bodies!"
