_ENV = Dora!

Item = Class Node,
	__init: =>
		@width = 144
		@height = 144
		@anchor = Vec2.zero

		@addChild with Sprite "Image/logo.png"
			.scaleX = 0.1
			.scaleY = 0.1
			.renderOrder = 1

		@addChild with DrawNode!
			\drawPolygon [
				Vec2 -60, -60
				Vec2 60, -60
				Vec2 60, 60
				Vec2 -60, 60
			], Color App.themeColor\toColor3!, 0x30
			.renderOrder = 2
			.angle = 45

		@addChild with Line [
				Vec2 -60, -60
				Vec2 60, -60
				Vec2 60, 60
				Vec2 -60, 60
				Vec2 -60, -60
			], Color 0xffff0080
			.renderOrder = 3
			.angle = 45

		@runAction Angle(5, 0, 360), true

currentEntry = with Node!
	.renderGroup = true
	.size = Size 750,750
	\addChild Item! for i = 1, 16
	\alignItems!

-- example codes ends here, some test ui below --

:renderGroup = currentEntry
windowFlags =
	* "NoDecoration"
	* "AlwaysAutoResize"
	* "NoSavedSettings"
	* "NoFocusOnAppearing"
	* "NoNav"
	* "NoMove"
threadLoop ->
	const :width = App.visualSize
	ImGui.SetNextWindowBgAlpha 0.35
	ImGui.SetNextWindowPos Vec2(width - 10, 10), "Always", Vec2 1, 0
	ImGui.SetNextWindowSize Vec2(240, 0), "FirstUseEver"
	ImGui.Begin "Render Group", windowFlags, ->
		ImGui.Text "Render Group (Yuescript)"
		ImGui.Separator!
		ImGui.TextWrapped "When render group is enabled, the nodes in the sub render tree will be grouped by \"renderOrder\" property, and get rendered in ascending order!\nNotice the draw call changes in stats window."
		if changed, renderGroup := ImGui.Checkbox "Grouped", renderGroup
			currentEntry.renderGroup = renderGroup
