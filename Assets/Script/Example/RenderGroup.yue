_ENV = Dorothy!

Item = Class Node,
	__init: =>
		@width = 144
		@height = 144
		@anchor = Vec2.zero

		@addChild with Sprite "Image/logo.png"
			.scaleX = 0.3
			.scaleY = 0.3
			.renderOrder = 1

		@addChild with DrawNode!
			\drawPolygon {
				Vec2 -60, -60
				Vec2 60, -60
				Vec2 60, 60
				Vec2 -60, 60
			},Color 0x3000ffff
			.renderOrder = 2
			.angle = 45

		@addChild with Line {
				Vec2 -60, -60
				Vec2 60, -60
				Vec2 60, 60
				Vec2 -60, 60
				Vec2 -60, -60
			},Color 0xffff0080
			.renderOrder = 3
			.angle = 45

		@runAction Angle 5,0,360
		@slot "ActionEnd", (action)-> @runAction action

currentEntry = with Node!
	.renderGroup = true
	.size = Size 750,750
	\addChild Item! for i = 1, 16
	\alignItems!
	\addTo Director.entry

-- example codes ends here, some test ui below --

_ENV = Dorothy builtin.ImGui

Director.entry\addChild with Node!
	:renderGroup = currentEntry
	windowFlags =
		* "NoDecoration"
		* "AlwaysAutoResize"
		* "NoSavedSettings"
		* "NoFocusOnAppearing"
		* "NoNav"
		* "NoMove"
	\schedule ->
		:width, :height = App.visualSize
		SetNextWindowBgAlpha 0.35
		SetNextWindowPos Vec2(width - 10, 10), "Always", Vec2 1,0
		SetNextWindowSize Vec2(240, 0), "FirstUseEver"
		Begin "Render Group", windowFlags, ->
			Text "Render Group"
			Separator!
			TextWrapped "When render group is enabled, the nodes in the sub render tree will be grouped by \"renderOrder\" property, and get rendered in ascending order!\nNotice the draw call changes in stats window."
			if changed, renderGroup = Checkbox "Grouped", renderGroup
				currentEntry.renderGroup = renderGroup
