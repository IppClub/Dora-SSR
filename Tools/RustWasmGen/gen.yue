#!yue -e
import "lulpeg"

lulpeg\global _G

cppTypes =
	i32: "int32_t"
	i64: "int64_t"
	f32: "float"
	f64: "double"

callbackDefs =
	string: "String"

interfaces =
	Node: true
	Camera: true
	Playable: true

inheritances =
	IPlayable: "INode"

basicTypes =
	uint8_t:
		* "i32"
		* (name)-> "s_cast<uint8_t>(#{name})"
		* (name)-> "s_cast<int32_t>(#{name})"
		* "i32"
		* "i32"
		* (name)-> name
		* (name)-> name
		* "stack.pop_i32().unwrap()"
		* (name)-> "stack.push_i32(#{name});"
	uint16_t:
		* "i32"
		* (name)-> "s_cast<uint16_t>(#{name})"
		* (name)-> "s_cast<int32_t>(#{name})"
		* "i32"
		* "i32"
		* (name)-> name
		* (name)-> name
		* "stack.pop_i32().unwrap()"
		* (name)-> "stack.push_i32(#{name});"
	uint32_t:
		* "i32"
		* (name)-> "s_cast<uint32_t>(#{name})"
		* (name)-> "s_cast<int32_t>(#{name})"
		* "i32"
		* "i32"
		* (name)-> name
		* (name)-> name
		* "stack.pop_i32().unwrap()"
		* (name)-> "stack.push_i32(#{name});"
	int32_t:
		* "i32"
		* (name)-> "s_cast<int32_t>(#{name})"
		* (name)-> "s_cast<int32_t>(#{name})"
		* "i32"
		* "i32"
		* (name)-> name
		* (name)-> name
		* "stack.pop_i32().unwrap()"
		* (name)-> "stack.push_i32(#{name});"
	int:
		* "i32"
		* (name)-> "s_cast<int>(#{name})"
		* (name)-> "s_cast<int32_t>(#{name})"
		* "i32"
		* "i32"
		* (name)-> name
		* (name)-> name
		* "stack.pop_i32().unwrap()"
		* (name)-> "stack.push_i32(#{name});"
	uint64_t:
		* "i64"
		* (name)-> "s_cast<uint64_t>(#{name})"
		* (name)-> "s_cast<int64_t>(#{name})"
		* "i64"
		* "i64"
		* (name)-> name
		* (name)-> name
		* "stack.pop_i64().unwrap()"
		* (name)-> "stack.push_i64(#{name});"
	int64_t:
		* "i64"
		* (name)-> "s_cast<int64_t>(#{name})"
		* (name)-> "s_cast<int64_t>(#{name})"
		* "i64"
		* "i64"
		* (name)-> name
		* (name)-> name
		* "stack.pop_i64().unwrap()"
		* (name)-> "stack.push_i64(#{name});"
	size_t:
		* "i64"
		* (name)-> "s_cast<int64_t>(#{name})"
		* (name)-> "s_cast<int64_t>(#{name})"
		* "i64"
		* "i64"
		* (name)-> name
		* (name)-> name
		* "stack.pop_i64().unwrap()"
		* (name)-> "stack.push_i64(#{name});"
	bool:
		* "i32" -- passing type
		* (name)-> "#{name} != 0" -- convert from cpp
		* (name)-> "#{name} ? 1 : 0" -- convert to cpp
		* "bool" -- function argument type
		* "bool" -- function return type
		* (name)-> "if #{name} { 1 } else { 0 }" -- convert from rust
		* (name)-> "#{name} != 0" -- convert to rust
		* "stack.pop_bool().unwrap()" -- callback return
		* (name)-> "stack.push_bool(#{name});" -- callback pass in
	float:
		* "f32"
		* (name)-> "s_cast<float>(#{name})"
		* (name)-> "s_cast<int32_t>(#{name})"
		* "f32"
		* "f32"
		* (name)-> name
		* (name)-> name
		* "stack.pop_f32().unwrap()"
		* (name)-> "stack.push_f32(#{name});"
	double:
		* "f64"
		* (name)-> "s_cast<double>(#{name})"
		* (name)-> "s_cast<double>(#{name})"
		* "f64"
		* "f64"
		* (name)-> name
		* (name)-> name
		* "stack.pop_f64().unwrap()"
		* (name)-> "stack.push_f64(#{name});"
	string:
		* "i64"
		* (name)-> "*str_from(#{name})"
		* (name)-> "str_retain(#{name})"
		* "&str"
		* "String"
		* (name)-> "crate::dora::from_string(#{name})"
		* (name)-> "crate::dora::to_string(#{name})"
		* "stack.pop_str().unwrap().as_str()"
		* (name)-> "stack.push_i64(#{name});"
	Vec2:
		* "i64"
		* (name)-> "vec2_from(#{name})"
		* (name)-> "vec2_retain(#{name})"
		* "&crate::dora::Vec2"
		* "crate::dora::Vec2"
		* (name)-> "#{name}.into_i64()"
		* (name)-> "crate::dora::Vec2::from(#{name})"
		* "&stack.pop_vec2().unwrap()"
		* (name)-> "stack.push_vec2(#{name});"
	Size:
		* "i64"
		* (name)-> "size_from(#{name})"
		* (name)-> "size_retain(#{name})"
		* "&crate::dora::Size"
		* "crate::dora::Size"
		* (name)-> "#{name}.into_i64()"
		* (name)-> "crate::dora::Size::from(#{name})"
		* "&stack.pop_size().unwrap()"
		* (name)-> "stack.push_size(#{name});"
	Color:
		* "i32"
		* (name)-> "Color(s_cast<uint32_t>(#{name}))"
		* (name)-> "#{name}.toARGB()"
		* "&crate::dora::Color"
		* "crate::dora::Color"
		* (name)-> "#{name}.to_argb() as i32"
		* (name)-> "crate::dora::Color::from(#{name})"
		* (name)-> error "unsupported"
		* (name)-> error "unsupported"
	Color3:
		* "i32"
		* (name)-> "Color3(s_cast<uint32_t>(#{name}))"
		* (name)-> "#{name}.toRGB()"
		* "&crate::dora::Color3"
		* "crate::dora::Color3"
		* (name)-> "#{name}.to_rgb() as i32"
		* (name)-> "crate::dora::Color3::from(#{name})"
		* (name)-> error "unsupported"
		* (name)-> error "unsupported"
	Rect:
		* "i64"
		* (name)-> "*r_cast<Rect*>(#{name})"
		* (name)-> "r_cast<int64_t>(new Rect{#{name}})"
		* "&crate::dora::Rect"
		* "crate::dora::Rect"
		* (name)-> "#{name}.raw()"
		* (name)-> "crate::dora::Rect::from(#{name})"
		* (name)-> error "unsupported"
		* (name)-> error "unsupported"
	VecStr:
		* "i64"
		* (name)-> "from_str_vec(#{name})"
		* (name)-> "to_vec(#{name})"
		* "&Vec<&str>"
		* "Vec<String>"
		* (name)-> "crate::dora::Vector::from_str(#{name})"
		* (name)-> "crate::dora::Vector::to_str(#{name})"
		* (name)-> error "unsupported"
		* (name)-> error "unsupported"
	VecVec2:
		* "i64"
		* (name)-> "from_vec2_vec(#{name})"
		* (name)-> "to_vec(#{name})"
		* "&Vec<crate::dora::Vec2>"
		* "Vec<crate::dora::Vec2>"
		* (name)-> "crate::dora::Vector::from_vec2(#{name})"
		* (name)-> "crate::dora::Vector::to_vec2(#{name})"
		* (name)-> error "unsupported"
		* (name)-> error "unsupported"
	VertexColor:
		* "i64"
		* (name)-> "*r_cast<VertexColor*>(#{name})"
		* (name)-> "r_cast<int64_t>(new VertexColor{#{name}})"
		* "&crate::dora::VertexColor"
		* "crate::dora::VertexColor"
		* (name)-> "#{name}.raw()"
		* (name)-> "crate::dora::VertexColor::from(#{name})"
		* (name)-> error "unsupported"
		* (name)-> error "unsupported"
	VecVertexColor:
		* "i64"
		* (name)-> "from_vertex_color_vec(#{name})"
		* (name)-> "to_vec(#{name})"
		* "&Vec<crate::dora::VertexColor>"
		* "Vec<crate::dora::VertexColor>"
		* (name)-> "crate::dora::Vector::from_vertex_color(#{name})"
		* (name)-> "crate::dora::Vector::to_vertex_color(#{name})"
		* (name)-> error "unsupported"
		* (name)-> error "unsupported"
	ActionDef:
		* "i64"
		* (name)-> "std::move(*r_cast<ActionDef*>(#{name}))"
		* (name)-> "r_cast<int64_t>(new ActionDef{#{name}})"
		* "&crate::dora::ActionDef"
		* "crate::dora::ActionDef"
		* (name)-> "#{name}.raw()"
		* (name)-> "crate::dora::ActionDef::from(#{name})"
		* (name)-> error "unsupported"
		* (name)-> error "unsupported"
	VecActionDef:
		* "i64"
		* (name)-> "from_action_def_vec(#{name})"
		* (name)-> "to_vec(#{name})"
		* "&Vec<crate::dora::ActionDef>"
		* "Vec<crate::dora::ActionDef>"
		* (name)-> "crate::dora::Vector::from_action_def(#{name})"
		* (name)-> "crate::dora::Vector::to_action_def(#{name})"
		* (name)-> error "unsupported"
		* (name)-> error "unsupported"
	BlendFunc:
		* "i64"
		* (name)-> "BlendFunc(s_cast<uint64_t>(#{name}))"
		* (name)-> "s_cast<int64_t>(#{name}.toValue())"
		* "u64"
		* "u64"
		* (name)-> "#{name} as i64"
		* (name)-> "#{name} as u64"
		* (name)-> error "unsupported"
		* (name)-> error "unsupported"
	EntityEvent:
		* "i32"
		* (name)-> "s_cast<int>(#{name})"
		* (name)-> "s_cast<int32_t>(#{name})"
		* "crate::dora::EntityEvent"
		* "crate::dora::EntityEvent"
		* (name)-> "#{name} as i32"
		* (name)-> "core::mem::transmute(#{name})"
		* (name)-> error "unsupported"
		* (name)-> error "unsupported"
	TextureWrap:
		* "i32"
		* (name)-> "s_cast<TextureWrap>(#{name})"
		* (name)-> "s_cast<int32_t>(#{name})"
		* "crate::dora::TextureWrap"
		* "crate::dora::TextureWrap"
		* (name)-> "#{name} as i32"
		* (name)-> "core::mem::transmute(#{name})"
		* (name)-> error "unsupported"
		* (name)-> error "unsupported"
	TextureFilter:
		* "i32"
		* (name)-> "s_cast<TextureFilter>(#{name})"
		* (name)-> "s_cast<int32_t>(#{name})"
		* "crate::dora::TextureFilter"
		* "crate::dora::TextureFilter"
		* (name)-> "#{name} as i32"
		* (name)-> "core::mem::transmute(#{name})"
		* (name)-> error "unsupported"
		* (name)-> error "unsupported"
	EaseType:
		* "i32"
		* (name)-> "s_cast<Ease::Enum>(#{name})"
		* (name)-> "s_cast<int32_t>(#{name})"
		* "crate::dora::EaseType"
		* "crate::dora::EaseType"
		* (name)-> "#{name} as i32"
		* (name)-> "core::mem::transmute(#{name})"
		* (name)-> error "unsupported"
		* (name)-> error "unsupported"
	Property:
		* "i32"
		* (name)-> "s_cast<Property::Enum>(#{name})"
		* (name)-> "s_cast<int32_t>(#{name})"
		* "crate::dora::Property"
		* "crate::dora::Property"
		* (name)-> "#{name} as i32"
		* (name)-> "core::mem::transmute(#{name})"
		* (name)-> error "unsupported"
		* (name)-> error "unsupported"
	TextAlign:
		* "i32"
		* (name)-> "s_cast<TextAlign>(#{name})"
		* (name)-> "s_cast<int32_t>(#{name})"
		* "crate::dora::TextAlign"
		* "crate::dora::TextAlign"
		* (name)-> "#{name} as i32"
		* (name)-> "core::mem::transmute(#{name})"
		* (name)-> error "unsupported"
		* (name)-> error "unsupported"
	void: {}

lastPos = 1

Newline = Cmt P"\n", (str, pos)->
	lastPos = pos
	true

White = (S" \t" + Newline)^0

AlphaNum = R "az", "AZ", "09", "__"

Name = C R("az", "AZ", "__") * AlphaNum^0

ClassLabel = C(P"singleton") + (C(P"interface")^-1 * White * C(P"object")) + C(P"value") + P""

FieldLabel = C(P"static")^-1 * White * C(P"optional")^-1 * White * C(P"readonly")^-1 * White * C(P"common" + P"boolean")^-1

MethodLabel = C(P"static")^-1 * White * C(P"outside")^-1 * White * C(P"optional")^-1

Type = C R("az", "AZ", "__") * AlphaNum^0 * (White * P"*")^-1

mark = (name) -> (...) -> {name, ...}

Param = P {
	"Param"
	Param: V"Func" * White * Name / mark"callback" + Type * White * Name / mark"variable"
	Func: Ct P"function<" * White * Type * White * Ct P"(" * White * (V"Param" * (White * P"," * White * V"Param")^0 * White)^-1 * P")" * White * P">"
}

Method = Ct(White * MethodLabel) * White * Type * White * (C(P"operator==") + Name) * White * (P"@" * White * Name + Cc false) * White * Ct(P"(" * White * (Param * (White * P"," * White * Param)^0 * White)^-1 * P")") * White * C(P"const")^-1 * White * P";" / mark"method"

Field = Ct(White * FieldLabel) * White * Type * White * Name * White * (P"@" * White * Name + Cc false) * White * P";" / mark"field"

Class = Ct Ct(White * ClassLabel) * White * (P"class" + P"struct") * White * Ct(Name * White * (P"@" * White * Name + Cc false) * White * (P":" * White * (P"public")^-1 * White * Name * White + Cc false)) *
	P"{" *
		Ct (White * (Method + Field))^0 * White *
	P"}" * White * P";"

File = Ct (White * Class)^1 * White * -1

codes = ""
with io.open "Dorothy.h", "r"
	codes = \read "*a"
	\close!

result = match File, codes

if not result
	if lastPos > #codes
		lastPos = #codes - 1

	line = 1
	begin = 0
	for i = 1, #codes
		break if i > lastPos
		if codes\sub(i, i) == '\n'
			line += 1
			begin = i

	following = codes\sub begin + 1
	lineStr = following\match("(.-)\n") or following\match ".*$"

	print "#{line}: syntax error:"
	print lineStr
else
	cppBinding = {}
	cppLink = {}
	rustExtern = {}
	rustBinding = {}
	nameMap = {}
	toCamelCase = (name)-> table.concat [sub\lower! for sub in (name\sub(1, 1)\lower! .. name\sub(2))\gsub("%d", "_%1")\gsub("%u", "_%1")\gsub("(%d)_", "%1")\gmatch "[^_]*"], "_"
	getObjectType = (dataType, rustName, isCreate, isOptional)->
		if isCreate
			return
				* "i64"
				* (name)-> "r_cast<#{dataType}*>(#{name})"
				* (name)-> "from_object(#{name})"
				* "&#{rustName}"
				* isOptional and "Option<#{rustName}>" or rustName
				* (name)-> "#{name}.raw()"
				* (name)-> isOptional and "#{rustName}::from(#{name})" or "#{rustName} { raw: #{name} }"
				* (name)-> error "unsupported"
				* (name)-> error "unsupported"
		elseif isOptional
			return
				* "i64"
				* (name)-> "r_cast<#{dataType}*>(#{name})"
				* (name)-> "from_object(#{name})"
				* interfaces[rustName] and "&dyn crate::dora::I#{rustName}" or "&crate::dora::#{rustName}"
				* "Option<crate::dora::#{rustName}>"
				* (name)-> "#{name}.raw()"
				* (name)-> "crate::dora::#{rustName}::from(#{name})"
				* "&stack.pop_cast::<crate::dora::#{rustName}>()"
				* (name)-> "stack.push_object(#{name});"
		else
			return
				* "i64"
				* (name)-> "r_cast<#{dataType}*>(#{name})"
				* (name)-> "from_object(#{name})"
				* interfaces[rustName] and "&dyn crate::dora::I#{rustName}" or "&crate::dora::#{rustName}"
				* "crate::dora::#{rustName}"
				* (name)-> "#{name}.raw()"
				* (name)-> "crate::dora::#{rustName}::from(#{name}).unwrap()"
				* "&stack.pop_cast::<crate::dora::#{rustName}>().unwrap()"
				* (name)-> "stack.push_object(#{name});"
	for cls in *result
		{clsLabels, clsNames, clsBody} = cls
		{clsName, clsNewName, clsParent} = clsNames
		nameMap[clsName] = clsNewName
		if clsParent
			clsParent = nameMap[clsParent] or clsParent
		clsNewName or= clsName
		isSingleton = false
		isObject = false
		isValue = false
		isTrait = false
		singletonName = nil
		for label in *clsLabels
			switch label
				when "singleton"
					isSingleton = true
					singletonName = "Shared#{clsName}"
				when "object"
					isObject = true
				when "value"
					isValue = true
				when "interface"
					isTrait = true
		moduleName = toCamelCase clsNewName
		clsNameL = clsName\lower!
		clsNewNameL = clsNewName\lower!
		rustBinding[] = "use crate::dora::Object;"
		objectUsed = #rustBinding
		if isObject
			objectUsed = nil
			cppBinding[] = "static int32_t #{clsNewNameL}_type()
{
	return DoraType<#{clsName}>();
}"
		if clsParent
			rustBinding[] = "use crate::dora::#{clsParent};"
			rustBinding[] = "impl #{clsParent} for #{clsNewName} { }"
		elseif isObject
			clsParent = "Object"
		if clsParent
			if inherit = inheritances[clsParent]
				rustBinding[] = "use crate::dora::#{inherit};"
				rustBinding[] = "impl #{inherit} for #{clsNewName} { }"
		cppLink[] = "static void link#{clsName}(wasm3::module& mod)\n{"
		rustBinding[] = if isTrait
			if isObject
				rustBinding[] = "pub struct #{clsNewName} { raw: i64 }
crate::dora_object!(#{clsNewName});
impl I#{clsNewName} for #{clsNewName} { }"
				cppLink[] = "\tmod.link_optional(\"*\", \"#{clsNewNameL}_type\", #{clsNewNameL}_type);"
			"pub trait I#{clsNewName}#{clsParent and ': ' .. clsParent or ''} {"
		else
			"pub struct #{clsNewName} { #{isSingleton and '' or 'raw: i64 '}}"
		if isValue
			rustBinding[] = "impl Drop for #{clsNewName} {
	fn drop(&mut self) { unsafe { #{clsNewNameL}_release(self.raw); } }
}"
		if isObject and not isTrait
			rustBinding[] = "crate::dora_object!(#{clsNewName});"
			cppLink[] = "\tmod.link_optional(\"*\", \"#{clsNewNameL}_type\", #{clsNewNameL}_type);"
		unless isTrait
			rustBinding[] = "impl #{clsNewName} {"
		rustExtern[] = "extern \"C\" {"
		if isObject
			rustExtern[] = "\tfn #{clsNewNameL}_type() -> i32;"
		if isValue
			cppBinding[] = "static void #{clsNewNameL}_release(int64_t raw)
{
	delete r_cast<#{clsName}*>(raw);
}"
			rustExtern[] = "\tfn #{clsNewNameL}_release(raw: i64);"
			rustBinding[] = "\tpub fn raw(&self) -> i64 {
		self.raw
	}
	pub fn from(raw: i64) -> #{clsNewName} {
		#{clsNewName} { raw: raw }
	}"
			cppLink[] = "\tmod.link_optional(\"*\", \"#{clsNewNameL}_release\", #{clsNewNameL}_release);"
		for clsItem in *clsBody
			pub = isTrait and "" or "pub "
			{itemType} = clsItem
			switch itemType
				when "field"
					{_, labels, dataType, name, newName} = clsItem
					funcNewName = newName or name
					funcName = table.concat [sub\lower! for sub in name\gsub("%u", "_%1")\gmatch "[^_]*"], "_"
					funcNewName = table.concat [sub\lower! for sub in funcNewName\gsub("%u", "_%1")\gmatch "[^_]*"], "_"
					isReadonly = false
					isStatic = false
					isOptional = false
					fieldType = "primitive"
					for label in *labels
						switch label
							when "readonly"
								isReadonly = true
							when "common", "boolean"
								fieldType = label
							when "static"
								isStatic = true
							when "optional"
								isOptional = true
					rustType = basicTypes[dataType]
					if rustType == nil and "*" == dataType\sub -1
						dataTypeName = dataType\match "[^ \t*]+"
						rustName = nameMap[dataTypeName] or dataTypeName
						rustType = getObjectType dataTypeName, rustName, false, isOptional
						if not isReadonly
							objectUsed = nil
					if rustType
						{rt, cast, castFrom, rtTypeIn, rtTypeOut, rtCast, rtCastFrom} = rustType
						cppType = cppTypes[rt]
						if not isReadonly
							setFunc = switch fieldType
								when "primitive"
									"#{name} = #{cast 'var'}"
								when "common", "boolean"
									if dataType == cppType
										"set#{name\sub(1, 1)\upper!}#{name\sub 2}(var)"
									else
										"set#{name\sub(1, 1)\upper!}#{name\sub 2}(#{cast 'var'})"
							cppSetterName = "#{clsNameL}_set_#{funcNewName}"
							rustSetterName = "set_#{funcNewName}"
							cppLink[] = "\tmod.link_optional(\"*\", \"#{cppSetterName}\", #{cppSetterName});"
							cppBinding[] = if isSingleton
								callFunc = isStatic and "#{clsName}::#{setFunc};" or "#{singletonName}.#{setFunc};"
								"static void #{cppSetterName}(#{cppType} var)\n{\n\t#{callFunc}\n}"
							else
								"static void #{cppSetterName}(int64_t self, #{cppType} var)\n{\n\tr_cast<#{clsName}*>(self)->#{setFunc};\n}"
							rustExtern[] = if isSingleton
								"\tfn #{cppSetterName}(var: #{rt});"
							else
								slf = isStatic and "" or "slf: i64, "
								"\tfn #{cppSetterName}(#{slf}var: #{rt});"
							rustBinding[] = if isSingleton
								"\t#{pub}fn #{rustSetterName}(var: #{rtTypeIn or rt}) {\n\t\tunsafe { #{cppSetterName}(#{rtCast and rtCast('var') or 'var'}) };\n\t}"
							else
								slfDecl = isStatic and "" or "&mut self, "
								slf = isStatic and "" or "self.raw(), "
								"\t#{pub}fn #{rustSetterName}(#{slfDecl}var: #{rtTypeIn or rt}) {\n\t\tunsafe { #{cppSetterName}(#{slf}#{rtCast and rtCast('var') or 'var'}) };\n\t}"
						getFunc, prefix = switch fieldType
							when "primitive"
								"#{name}", "get"
							when "common"
								"get#{name\sub(1, 1)\upper!}#{name\sub 2}()", "get"
							when "boolean"
								"is#{name\sub(1, 1)\upper!}#{name\sub 2}()", "is"
						if dataType == cppType
							castFrom = (name)-> name
						cppGetterName = "#{clsNameL}_#{prefix}_#{funcNewName}"
						rustGetterName = "#{prefix}_#{funcNewName}"
						cppLink[] = "\tmod.link_optional(\"*\", \"#{cppGetterName}\", #{cppGetterName});"
						cppBinding[] = if isSingleton
							callFunc = isStatic and "#{clsName}::#{getFunc}" or "#{singletonName}.#{getFunc}"
							item = castFrom "#{callFunc}"
							"static #{cppType} #{cppGetterName}()\n{\n\treturn #{item};\n}"
						else
							callFunc = isStatic and "#{clsName}::#{getFunc}" or "r_cast<#{clsName}*>(self)->#{getFunc}"
							item = castFrom callFunc
							"static #{cppType} #{cppGetterName}(int64_t self)\n{\n\treturn #{item};\n}"
						rustExtern[] = if isSingleton
							"\tfn #{cppGetterName}() -> #{rt};"
						else
							slf = isStatic and "" or "slf: i64"
							"\tfn #{cppGetterName}(#{slf}) -> #{rt};"
						rustBinding[] = if isSingleton
							item = "#{cppGetterName}()"
							"\t#{pub}fn #{rustGetterName}() -> #{rtTypeOut or rt} {\n\t\treturn unsafe { #{rtCastFrom and rtCastFrom(item) or item} };\n\t}"
						else
							slfDecl = isStatic and "" or "&self"
							slf = isStatic and "" or "self.raw()"
							item = "#{cppGetterName}(#{slf})"
							"\t#{pub}fn #{rustGetterName}(#{slfDecl}) -> #{rtTypeOut or rt} {\n\t\treturn unsafe { #{rtCastFrom and rtCastFrom(item) or item} };\n\t}"
					else
						error "\"#{dataType}\" is not supported."
				when "method"
					{_, labels, dataType, name, newName, args, constFlag} = clsItem
					funcNewName = newName or name
					funcName = table.concat [sub\lower! for sub in name\gsub("%u", "_%1")\gmatch "[^_]*"], "_"
					isCreate = false
					if "create" == funcNewName\sub 1, 6
						isCreate = true
						funcNewName = if #funcNewName > 6
							"with" .. funcNewName\sub 7
						else
							"new"
						pub = "pub "
					funcNewName = table.concat [sub\lower! for sub in funcNewName\gsub("%u", "_%1")\gmatch "[^_]*"], "_"
					isStatic = false
					isOutside = false
					isOptional = false
					isConst = constFlag == "const"
					for label in *labels
						switch label
							when "outside"
								isOutside = true
							when "static"
								isStatic = true
							when "optional"
								isOptional = true
					rustType = basicTypes[dataType]
					if rustType == nil and "*" == dataType\sub -1
						dataTypeName = dataType\match "[^ \t*]+"
						rustName = nameMap[dataTypeName] or dataTypeName
						rustType = getObjectType dataTypeName, rustName, isCreate, isOptional
						objectUsed = nil
					if rustType
						{rt, cast, castFrom, _, rtTypeOut, _, rtCastFrom} = rustType
						cppType = cppTypes[rt] ?? "void"
						argItems = for arg in *args
							{argKind, argType, argName} = arg
							argName = toCamelCase argName
							switch argKind
								when "variable"
									rustTypeA = basicTypes[argType]
									if rustTypeA == nil and "*" == argType\sub -1
										argTypeName = argType\match "[^ \t*]+"
										rustName = nameMap[argTypeName] or argTypeName
										rustTypeA = getObjectType argTypeName, rustName
										objectUsed = nil
									if rustTypeA
										{rtA, castA, _, rtTypeInA, _, rtCastA} = rustTypeA
										cppTypeA = cppTypes[rtA]
										{
											"#{cppTypeA} #{argName}"
											(argType == cppTypeA and argName or castA argName)
											"#{argName}: #{rtA}"
											"#{argName}: #{rtTypeInA or rtA}"
											"#{rtCastA and rtCastA(argName) or argName}"
										}
									else
										error "\"#{argType}\" is not supported."
								when "callback"
									{_, func, fname} = arg
									{freturnType, fargs} = func
									items = for farg in *fargs
										{fargKind, fargType, fargName} = farg
										if fargKind ~= "variable"
											error "\"#{fargKind}\" is not supported in function argument."
										frustType = basicTypes[fargType]
										if frustType == nil and "*" == fargType\sub -1
											fargTypeName = fargType\match "[^ \t*]+"
											frustName = nameMap[fargTypeName] or fargTypeName
											frustType = getObjectType fargTypeName, frustName
										if frustType
											{frt, _2, _3, frtTypeIn, _5, _6, _7, fpopArg} = frustType
											fcppType = callbackDefs[fargType] or fargType
											if "Event" == fargType\match "[^ \t*]+"
												{
													"Event* #{fargName}"
													"\t\t#{fargName}->pushArgsToWasm(args);"
													"&mut crate::dora::CallStack"
													"&mut stack"
												}
											else
												{
													"#{fcppType} #{fargName}"
													"\t\targs->push(#{fargName});"
													frtTypeIn
													fpopArg
												}
										else
											error "\"#{fargType}\" is not supported."
									argPassed = #items > 0
									cppArgDef = table.concat [item[1] for item in *items], ", "
									cppArgPass = table.concat [item[2] for item in *items], "\n"
									if cppArgPass ~= ""
										cppArgPass = "\n#{cppArgPass}"
									callbackType = "dyn FnMut(#{table.concat [item[3] for item in *items], ', '})"
									callback = "#{fname}(#{table.concat [item[4] for item in *items], ', '})"
									callbackReturn = ""
									frRetType = basicTypes[freturnType]
									if frRetType == nil and "*" == freturnType\sub -1
										freturnType = freturnType\sub 1, -2
										rustName = nameMap[freturnType] or freturnType
										frRetType = getObjectType freturnType, rustName
									if frRetType
										{frt, _2, _3, _4, frtTypeOut, _6, _7, _8, frPush} = frRetType
										callbackType = if frtTypeOut
											"Box<#{callbackType} -> #{frtTypeOut}>"
										else
											"Box<#{callbackType}>"
										if frCppType = cppTypes[frt]
											callbackReturn = "\t\treturn std::get<#{freturnType}>(args->pop());\n"
											callback = "let result = #{callback};\n\t\t\t#{frPush 'result'}"
											argPassed = true
									{
										"int32_t func#{argPassed and ', int64_t stack' or ''}"
										"[func, #{argPassed and 'args, ' or ''}deref](#{cppArgDef})
	{
#{argPassed and '\t\targs->clear();' or ''}#{cppArgPass}
		SharedWasmRuntime.invoke(func);
#{callbackReturn}	}"
										"func: i32#{argPassed and ', stack: i64' or ''}"
										"mut #{fname}: #{callbackType}"
										"func_id#{argPassed and ', stack_raw' or ''}"
										"
	std::shared_ptr<void> deref(nullptr, [func](auto)
	{
		SharedWasmRuntime.deref(func);
	});#{argPassed and '\n\tauto args = r_cast<CallStack*>(stack);' or ''}"
										(argPassed and "\t\tlet mut stack = crate::dora::CallStack::new();
		let stack_raw = stack.raw();\n" or '') .. "\t\tlet func_id = crate::dora::push_function(Box::new(move || {
			#{callback}
		}));
"
									}
						argDefs = table.concat [item[1] for item in *argItems], ", "
						argPass = table.concat [item[2] for item in *argItems], ", "
						argRtDefs = table.concat [item[3] for item in *argItems], ", "
						argRtInDefs = table.concat [item[4] for item in *argItems], ", "
						argRtPass = table.concat [item[5] for item in *argItems], ", "
						argPrepare = table.concat [item[6] for item in *argItems when item[6]?]
						argRtPrepare = table.concat [item[7] for item in *argItems when item[7]?]
						if dataType == cppType
							castFrom = (name)-> name
						prefix = if cppType == "void" then "" else "return "
						cppFuncName = "#{clsNameL}_#{funcNewName}"
						rustFuncName = funcNewName
						cppLink[] = "\tmod.link_optional(\"*\", \"#{cppFuncName}\", #{cppFuncName});"
						cppBinding[] = if isSingleton
							callFunc = isStatic and "#{clsName}::#{name}(#{argPass})" or "#{singletonName}.#{name}(#{argPass})"
							callFunc = isOutside and "#{name}(#{argPass})" or callFunc
							item = castFrom callFunc
							"static #{cppType} #{cppFuncName}(#{argDefs})\n{#{argPrepare}\n\t#{prefix}#{item};\n}"
						else
							slf = isStatic and "" or "int64_t self"
							if slf ~= "" then argDefs = slf .. (argDefs ~= "" and ", " or "") .. argDefs
							callFunc = if isOutside
								if isStatic
									"#{name}(#{argPass})"
								else
									"#{name}(r_cast<#{clsName}*>(self)#{argPass == '' and '' or ', ' .. argPass})"
							else
								if isStatic
									"#{clsName}::#{name}(#{argPass})"
								else
									"r_cast<#{clsName}*>(self)->#{name}(#{argPass})"
							callFunc = isValue and argPass or callFunc if isCreate
							item = castFrom callFunc
							"static #{cppType} #{cppFuncName}(#{argDefs})\n{#{argPrepare}\n\t#{prefix}#{item};\n}"
						rustExtern[] = if isSingleton
							"\tfn #{cppFuncName}(#{argRtDefs})#{rt and ' -> ' .. rt or ''};"
						else
							slf = isStatic and "" or "slf: i64"
							if slf ~= "" then argRtDefs = slf .. (argRtDefs ~= "" and ", " or "") .. argRtDefs
							"\tfn #{cppFuncName}(#{argRtDefs})#{rt and ' -> ' .. rt or ''};"
						rustBinding[] = if isSingleton
							rtOut = rtTypeOut or rt
							item = "unsafe { #{cppFuncName}(#{argRtPass}) }"
							"\t#{pub}fn #{rustFuncName}(#{argRtInDefs})#{rtOut and ' -> ' .. rtOut or ''} {\n#{argRtPrepare}\t\t#{prefix}#{rtCastFrom and rtCastFrom(item) or item};\n\t}"
						else
							rtOut = rtTypeOut or rt
							slfParam = isConst and "&self" or "&mut self"
							slfDecl = isStatic and "" or slfParam
							if slfDecl ~= "" then argRtInDefs = slfDecl .. (argRtInDefs ~= "" and ", " or "") .. argRtInDefs
							slf = isStatic and "" or "self.raw()"
							if slf ~= "" then argRtPass = slf .. (argRtPass ~= "" and ", " or "") .. argRtPass
							item = "unsafe { #{cppFuncName}(#{argRtPass}) }"
							if funcNewName == "equals"
								table.insert rustBinding, objectUsed and objectUsed + 1 or 2, "impl PartialEq for Rect {
	fn eq(&self, other: &Self) -> bool {\n#{argRtPrepare}\t\t#{prefix}#{rtCastFrom and rtCastFrom(item) or item}\n\t}
}"
								nil
							else
								"\t#{pub}fn #{rustFuncName}(#{argRtInDefs})#{rtOut and ' -> ' .. rtOut or ''} {\n#{argRtPrepare}\t\t#{prefix}#{rtCastFrom and rtCastFrom(item) or item};\n\t}"
					else
						error "\"#{dataType}\" is not supported."
		if objectUsed
			table.remove rustBinding, objectUsed
		rustExtern[] = "}"
		if isTrait
			createFuncs = [item for item in *rustBinding when item\match("fn new") or item\match("fn with")]
			rustBinding = [item for item in *rustBinding when not item\match("fn new") and not item\match("fn with")]
			if #createFuncs > 0
				rustBinding[] = "}"
				rustBinding[] = "impl #{clsNewName} {"
				for func in *createFuncs
					rustBinding[] = func
		rustBinding[] = "}"
		cppLink[] = "}"
		close _f1 = with io.open "../../Source/Wasm/Dora/#{clsName}Wasm.hpp", "w"
			\write table.concat cppBinding, "\n"
			\write "\n"
			\write table.concat cppLink, "\n"
		close _f2 = with io.open "../dora-rust/src/dora/#{moduleName}.rs", "w"
			\write table.concat rustExtern, "\n"
			\write "\n"
			\write table.concat rustBinding, "\n"
		rustExtern = {}
		rustBinding = {}
		cppBinding = {}
		cppLink = {}
	print "Done!"

