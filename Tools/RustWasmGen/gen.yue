#!yue -e

licenseText = [[/* Copyright (c) 2024 Li Jin, dragon-fly@qq.com

Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE. */]]

import "lulpeg"

lulpeg\global _G

nonObjectTypes =
	FixtureDef: true

cppTypes =
	i32: "int32_t"
	i64: "int64_t"
	f32: "float"
	f64: "double"

callbackDefs =
	string: "String"

interfaces =
	Object: true
	Node: true
	Joint: true
	Camera: true
	Playable: true
	PhysicsWorld: true
	Body: true

inheritances =
	IPlayable: {"INode"}
	IPhysicsWorld: {"INode"}
	IBody: {"INode"}

basicTypes =
	uint8_t:
		* "i32"
		* (name)-> "s_cast<uint8_t>(#{name})"
		* (name)-> "s_cast<int32_t>(#{name})"
		* "i32"
		* "i32"
		* (name)-> name
		* (name)-> name
		* (fnArgId)-> "stack#{fnArgId}.pop_i32().unwrap()"
		* (name, fnArgId)-> "stack#{fnArgId}.push_i32(#{name});"
	uint16_t:
		* "i32"
		* (name)-> "s_cast<uint16_t>(#{name})"
		* (name)-> "s_cast<int32_t>(#{name})"
		* "i32"
		* "i32"
		* (name)-> name
		* (name)-> name
		* (fnArgId)-> "stack#{fnArgId}.pop_i32().unwrap()"
		* (name, fnArgId)-> "stack#{fnArgId}.push_i32(#{name});"
	uint32_t:
		* "i32"
		* (name)-> "s_cast<uint32_t>(#{name})"
		* (name)-> "s_cast<int32_t>(#{name})"
		* "i32"
		* "i32"
		* (name)-> name
		* (name)-> name
		* (fnArgId)-> "stack#{fnArgId}.pop_i32().unwrap()"
		* (name, fnArgId)-> "stack#{fnArgId}.push_i32(#{name});"
	int32_t:
		* "i32"
		* (name)-> "s_cast<int32_t>(#{name})"
		* (name)-> "s_cast<int32_t>(#{name})"
		* "i32"
		* "i32"
		* (name)-> name
		* (name)-> name
		* (fnArgId)-> "stack#{fnArgId}.pop_i32().unwrap()"
		* (name, fnArgId)-> "stack#{fnArgId}.push_i32(#{name});"
	int:
		* "i32"
		* (name)-> "s_cast<int>(#{name})"
		* (name)-> "s_cast<int32_t>(#{name})"
		* "i32"
		* "i32"
		* (name)-> name
		* (name)-> name
		* (fnArgId)-> "stack#{fnArgId}.pop_i32().unwrap()"
		* (name, fnArgId)-> "stack#{fnArgId}.push_i32(#{name});"
	uint64_t:
		* "i64"
		* (name)-> "s_cast<uint64_t>(#{name})"
		* (name)-> "s_cast<int64_t>(#{name})"
		* "i64"
		* "i64"
		* (name)-> name
		* (name)-> name
		* (fnArgId)-> "stack#{fnArgId}.pop_i64().unwrap()"
		* (name, fnArgId)-> "stack#{fnArgId}.push_i64(#{name});"
	int64_t:
		* "i64"
		* (name)-> "s_cast<int64_t>(#{name})"
		* (name)-> "s_cast<int64_t>(#{name})"
		* "i64"
		* "i64"
		* (name)-> name
		* (name)-> name
		* (fnArgId)-> "stack#{fnArgId}.pop_i64().unwrap()"
		* (name, fnArgId)-> "stack#{fnArgId}.push_i64(#{name});"
	size_t:
		* "i64"
		* (name)-> "s_cast<int64_t>(#{name})"
		* (name)-> "s_cast<int64_t>(#{name})"
		* "i64"
		* "i64"
		* (name)-> name
		* (name)-> name
		* (fnArgId)-> "stack#{fnArgId}.pop_i64().unwrap()"
		* (name, fnArgId)-> "stack#{fnArgId}.push_i64(#{name});"
	bool:
		* "i32" -- passing type
		* (name)-> "#{name} != 0" -- convert from cpp
		* (name)-> "#{name} ? 1 : 0" -- convert to cpp
		* "bool" -- function argument type
		* "bool" -- function return type
		* (name)-> "if #{name} { 1 } else { 0 }" -- convert from rust
		* (name)-> "#{name} != 0" -- convert to rust
		* (fnArgId)-> "stack#{fnArgId}.pop_bool().unwrap()" -- callback return
		* (name, fnArgId)-> "stack#{fnArgId}.push_bool(#{name});" -- callback pass in
	float:
		* "f32"
		* (name)-> "s_cast<float>(#{name})"
		* (name)-> "s_cast<int32_t>(#{name})"
		* "f32"
		* "f32"
		* (name)-> name
		* (name)-> name
		* (fnArgId)-> "stack#{fnArgId}.pop_f32().unwrap()"
		* (name, fnArgId)-> "stack#{fnArgId}.push_f32(#{name});"
	double:
		* "f64"
		* (name)-> "s_cast<double>(#{name})"
		* (name)-> "s_cast<double>(#{name})"
		* "f64"
		* "f64"
		* (name)-> name
		* (name)-> name
		* (fnArgId)-> "stack#{fnArgId}.pop_f64().unwrap()"
		* (name, fnArgId)-> "stack#{fnArgId}.push_f64(#{name});"
	string:
		* "i64"
		* (name)-> "*str_from(#{name})"
		* (name)-> "str_retain(#{name})"
		* "&str"
		* "String"
		* (name)-> "crate::dora::from_string(#{name})"
		* (name)-> "crate::dora::to_string(#{name})"
		* (fnArgId)-> "stack#{fnArgId}.pop_str().unwrap().as_str()"
		* (name, fnArgId)-> "stack#{fnArgId}.push_str(#{name}.as_str());"
	Vec2:
		* "i64"
		* (name)-> "vec2_from(#{name})"
		* (name)-> "vec2_retain(#{name})"
		* "&crate::dora::Vec2"
		* "crate::dora::Vec2"
		* (name)-> "#{name}.into_i64()"
		* (name)-> "crate::dora::Vec2::from(#{name})"
		* (fnArgId)-> "&stack#{fnArgId}.pop_vec2().unwrap()"
		* (name, fnArgId)-> "stack#{fnArgId}.push_vec2(#{name});"
	Size:
		* "i64"
		* (name)-> "size_from(#{name})"
		* (name)-> "size_retain(#{name})"
		* "&crate::dora::Size"
		* "crate::dora::Size"
		* (name)-> "#{name}.into_i64()"
		* (name)-> "crate::dora::Size::from(#{name})"
		* (fnArgId)-> "&stack#{fnArgId}.pop_size().unwrap()"
		* (name, fnArgId)-> "stack#{fnArgId}.push_size(#{name});"
	Color:
		* "i32"
		* (name)-> "Color(s_cast<uint32_t>(#{name}))"
		* (name)-> "#{name}.toARGB()"
		* "&crate::dora::Color"
		* "crate::dora::Color"
		* (name)-> "#{name}.to_argb() as i32"
		* (name)-> "crate::dora::Color::from(#{name})"
		* -> error "unsupported"
		* -> error "unsupported"
	Color3:
		* "i32"
		* (name)-> "Color3(s_cast<uint32_t>(#{name}))"
		* (name)-> "#{name}.toRGB()"
		* "&crate::dora::Color3"
		* "crate::dora::Color3"
		* (name)-> "#{name}.to_rgb() as i32"
		* (name)-> "crate::dora::Color3::from(#{name})"
		* -> error "unsupported"
		* -> error "unsupported"
	Rect:
		* "i64"
		* (name)-> "*r_cast<Rect*>(#{name})"
		* (name)-> "r_cast<int64_t>(new Rect{#{name}})"
		* "&crate::dora::Rect"
		* "crate::dora::Rect"
		* (name)-> "#{name}.raw()"
		* (name)-> "crate::dora::Rect::from(#{name})"
		* -> error "unsupported"
		* -> error "unsupported"
	WasmActionUpdate:
		* "i64"
		* (name)-> "*r_cast<WasmActionUpdate*>(#{name})"
		* (name)-> "r_cast<int64_t>(new Platformer::WasmActionUpdate{#{name}})"
		* "&crate::dora::platformer::ActionUpdate"
		* "crate::dora::platformer::ActionUpdate"
		* (name)-> "#{name}.raw()"
		* (name)-> "crate::dora::platformer::ActionUpdate::from(#{name})"
		* (fnArgId)-> "crate::dora::platformer::ActionUpdate::from(stack#{fnArgId}.pop_i64().unwrap())"
		* (name, fnArgId)-> "stack#{fnArgId}.push_i64(#{name}.raw());"
	"Platformer::WasmActionUpdate":
		* "i64"
		* (name)-> "*r_cast<WasmActionUpdate*>(#{name})"
		* (name)-> "r_cast<int64_t>(new Platformer::WasmActionUpdate{#{name}})"
		* "&crate::dora::platformer::ActionUpdate"
		* "crate::dora::platformer::ActionUpdate"
		* (name)-> "#{name}.raw()"
		* (name)-> "crate::dora::platformer::ActionUpdate::from(#{name})"
		* (fnArgId)-> "crate::dora::platformer::ActionUpdate::from(stack#{fnArgId}.pop_i64().unwrap())"
		* (name, fnArgId)-> "stack#{fnArgId}.push_i64(#{name}.raw());"
	DBParams:
		* "i64"
		* (name)-> "*r_cast<DBParams*>(#{name})"
		* (name)-> "r_cast<int64_t>(new DBParams{})"
		* "crate::dora::DBParams"
		* "crate::dora::DBParams"
		* (name)-> "#{name}.raw()"
		* (name)-> "crate::dora::DBParams::from(#{name})"
		* (fnArgId)-> "crate::dora::DBParams::from(stack#{fnArgId}.pop_i64().unwrap())"
		* -> error "unsupported"
	DBRecord:
		* "i64"
		* (name)-> "*r_cast<DBRecord*>(#{name})"
		* (name)-> "r_cast<int64_t>(new DBRecord{#{name}})"
		* "crate::dora::DBRecord"
		* "crate::dora::DBRecord"
		* (name)-> "#{name}.raw()"
		* (name)-> "crate::dora::DBRecord::from(#{name})"
		* (fnArgId)-> "crate::dora::DBRecord::from(stack#{fnArgId}.pop_i64().unwrap())"
		* -> error "unsupported"
	DBQuery:
		* "i64"
		* (name)-> "*r_cast<DBQuery*>(#{name})"
		* (name)-> "r_cast<int64_t>(new DBQuery{})"
		* "crate::dora::DBQuery"
		* "crate::dora::DBQuery"
		* (name)-> "#{name}.raw()"
		* (name)-> "crate::dora::DBQuery::from(#{name})"
		* -> error "unsupported"
		* -> error "unsupported"
	VecStr:
		* "i64"
		* (name)-> "from_str_vec(#{name})"
		* (name)-> "to_vec(#{name})"
		* "&Vec<&str>"
		* "Vec<String>"
		* (name)-> "crate::dora::Vector::from_str(#{name})"
		* (name)-> "crate::dora::Vector::to_str(#{name})"
		* -> error "unsupported"
		* -> error "unsupported"
	VecVec2:
		* "i64"
		* (name)-> "from_vec2_vec(#{name})"
		* (name)-> "to_vec(#{name})"
		* "&Vec<crate::dora::Vec2>"
		* "Vec<crate::dora::Vec2>"
		* (name)-> "crate::dora::Vector::from_vec2(#{name})"
		* (name)-> "crate::dora::Vector::to_vec2(#{name})"
		* -> error "unsupported"
		* -> error "unsupported"
	VecUint32:
		* "i64"
		* (name)-> "from_uint32_vec(#{name})"
		* (name)-> "to_vec(#{name})"
		* "&Vec<i32>"
		* "Vec<i32>"
		* (name)-> "crate::dora::Vector::from_i32(#{name})"
		* (name)-> "crate::dora::Vector::to_i32(#{name})"
		* -> error "unsupported"
		* -> error "unsupported"
	VecFloat:
		* "i64"
		* (name)-> "from_float_vec(#{name})"
		* (name)-> "to_vec(#{name})"
		* "&Vec<f32>"
		* "Vec<f32>"
		* (name)-> "crate::dora::Vector::from_f32(#{name})"
		* (name)-> "crate::dora::Vector::to_f32(#{name})"
		* -> error "unsupported"
		* -> error "unsupported"
	VertexColor:
		* "i64"
		* (name)-> "*r_cast<VertexColor*>(#{name})"
		* (name)-> "r_cast<int64_t>(new VertexColor{#{name}})"
		* "&crate::dora::VertexColor"
		* "crate::dora::VertexColor"
		* (name)-> "#{name}.raw()"
		* (name)-> "crate::dora::VertexColor::from(#{name})"
		* -> error "unsupported"
		* -> error "unsupported"
	VecVertexColor:
		* "i64"
		* (name)-> "from_vertex_color_vec(#{name})"
		* (name)-> "to_vec(#{name})"
		* "&Vec<crate::dora::VertexColor>"
		* "Vec<crate::dora::VertexColor>"
		* (name)-> "crate::dora::Vector::from_vertex_color(#{name})"
		* (name)-> "crate::dora::Vector::to_vertex_color(#{name})"
		* -> error "unsupported"
		* -> error "unsupported"
	ActionDef:
		* "i64"
		* (name)-> "std::move(*r_cast<ActionDef*>(#{name}))"
		* (name)-> "r_cast<int64_t>(new ActionDef{#{name}})"
		* "crate::dora::ActionDef"
		* "crate::dora::ActionDef"
		* (name)-> "#{name}.raw()"
		* (name)-> "crate::dora::ActionDef::from(#{name})"
		* -> error "unsupported"
		* -> error "unsupported"
	VecActionDef:
		* "i64"
		* (name)-> "from_action_def_vec(#{name})"
		* (name)-> "to_vec(#{name})"
		* "&Vec<crate::dora::ActionDef>"
		* "Vec<crate::dora::ActionDef>"
		* (name)-> "crate::dora::Vector::from_action_def(#{name})"
		* -> error "unsupported"
		* -> error "unsupported"
		* -> error "unsupported"
	VecBTree:
		* "i64"
		* (name)-> "from_btree_vec(#{name})"
		* (name)-> "to_vec(#{name})"
		* "&Vec<crate::dora::platformer::behavior::Tree>"
		* "Vec<crate::dora::platformer::behavior::Tree>"
		* (name)-> "crate::dora::Vector::from_btree(#{name})"
		* -> error "unsupported"
		* -> error "unsupported"
		* -> error "unsupported"
	VecDTree:
		* "i64"
		* (name)-> "from_dtree_vec(#{name})"
		* (name)-> "to_vec(#{name})"
		* "&Vec<crate::dora::platformer::decision::Tree>"
		* "Vec<crate::dora::platformer::decision::Tree>"
		* (name)-> "crate::dora::Vector::from_dtree(#{name})"
		* -> error "unsupported"
		* -> error "unsupported"
		* -> error "unsupported"
	BlendFunc:
		* "i64"
		* (name)-> "BlendFunc(s_cast<uint64_t>(#{name}))"
		* (name)-> "s_cast<int64_t>(#{name}.toValue())"
		* "u64"
		* "u64"
		* (name)-> "#{name} as i64"
		* (name)-> "#{name} as u64"
		* -> error "unsupported"
		* -> error "unsupported"
	MLQState:
		* "i64"
		* (name)-> "s_cast<MLQState>(#{name})"
		* (name)-> "s_cast<int64_t>(#{name})"
		* "u64"
		* "u64"
		* (name)-> "#{name} as i64"
		* (name)-> "#{name} as u64"
		* (fnArgId)-> "stack#{fnArgId}.pop_i64().unwrap() as u64"
		* -> error "unsupported"
	MLQAction:
		* "i32"
		* (name)-> "s_cast<MLQAction>(#{name})"
		* (name)-> "s_cast<int32_t>(#{name})"
		* "u32"
		* "u32"
		* (name)-> "#{name} as i32"
		* (name)-> "#{name} as u32"
		* (fnArgId)-> "stack#{fnArgId}.pop_i32().unwrap() as u32"
		* -> error "unsupported"
	"Platformer::TargetAllow":
		* "i64"
		* (name)-> "*r_cast<Platformer::TargetAllow*>(#{name})"
		* (name)-> "r_cast<int64_t>(new Platformer::TargetAllow{#{name}})"
		* "&crate::dora::platformer::TargetAllow"
		* "crate::dora::platformer::TargetAllow"
		* (name)-> "#{name}.raw()"
		* (name)-> "crate::dora::platformer::TargetAllow::from(#{name})"
		* -> error "unsupported"
		* -> error "unsupported"
	EntityEvent:
		* "i32"
		* (name)-> "s_cast<int>(#{name})"
		* (name)-> "s_cast<int32_t>(#{name})"
		* "crate::dora::EntityEvent"
		* "crate::dora::EntityEvent"
		* (name)-> "#{name} as i32"
		* (name)-> "core::mem::transmute(#{name})"
		* -> error "unsupported"
		* -> error "unsupported"
	TextureWrap:
		* "i32"
		* (name)-> "s_cast<TextureWrap>(#{name})"
		* (name)-> "s_cast<int32_t>(#{name})"
		* "crate::dora::TextureWrap"
		* "crate::dora::TextureWrap"
		* (name)-> "#{name} as i32"
		* (name)-> "core::mem::transmute(#{name})"
		* -> error "unsupported"
		* -> error "unsupported"
	TextureFilter:
		* "i32"
		* (name)-> "s_cast<TextureFilter>(#{name})"
		* (name)-> "s_cast<int32_t>(#{name})"
		* "crate::dora::TextureFilter"
		* "crate::dora::TextureFilter"
		* (name)-> "#{name} as i32"
		* (name)-> "core::mem::transmute(#{name})"
		* -> error "unsupported"
		* -> error "unsupported"
	EaseType:
		* "i32"
		* (name)-> "s_cast<Ease::Enum>(#{name})"
		* (name)-> "s_cast<int32_t>(#{name})"
		* "crate::dora::EaseType"
		* "crate::dora::EaseType"
		* (name)-> "#{name} as i32"
		* (name)-> "core::mem::transmute(#{name})"
		* -> error "unsupported"
		* -> error "unsupported"
	Property:
		* "i32"
		* (name)-> "s_cast<Property::Enum>(#{name})"
		* (name)-> "s_cast<int32_t>(#{name})"
		* "crate::dora::Property"
		* "crate::dora::Property"
		* (name)-> "#{name} as i32"
		* (name)-> "core::mem::transmute(#{name})"
		* -> error "unsupported"
		* -> error "unsupported"
	TextAlign:
		* "i32"
		* (name)-> "s_cast<TextAlign>(#{name})"
		* (name)-> "s_cast<int32_t>(#{name})"
		* "crate::dora::TextAlign"
		* "crate::dora::TextAlign"
		* (name)-> "#{name} as i32"
		* (name)-> "core::mem::transmute(#{name})"
		* -> error "unsupported"
		* -> error "unsupported"
	"Platformer::Relation":
		* "i32"
		* (name)-> "s_cast<Platformer::Relation>(#{name})"
		* (name)-> "s_cast<int32_t>(#{name})"
		* "crate::dora::platformer::Relation"
		* "crate::dora::platformer::Relation"
		* (name)-> "#{name} as i32"
		* (name)-> "core::mem::transmute(#{name})"
		* -> error "unsupported"
		* -> error "unsupported"
	"Platformer::Behavior::Blackboard":
		* "i64"
		* (name)-> "*r_cast<Platformer::Behavior::Blackboard*>(#{name})"
		* (name)-> "r_cast<int64_t>(#{name})"
		* "&crate::dora::platformer::behavior::Blackboard"
		* "crate::dora::platformer::behavior::Blackboard"
		* (name)-> "#{name}.raw()"
		* (name)-> "crate::dora::platformer::behavior::Blackboard::from(#{name})"
		* (fnArgId)-> "&crate::dora::platformer::behavior::Blackboard::from(stack#{fnArgId}.pop_i64().unwrap()).unwrap()"
		* -> error "unsupported"
	"Platformer::UnitAction":
		* "i64"
		* (name)-> "*r_cast<Platformer::UnitAction*>(#{name})"
		* (name)-> "r_cast<int64_t>(#{name})"
		* "&crate::dora::platformer::UnitAction"
		* "crate::dora::platformer::UnitAction"
		* (name)-> "#{name}.raw()"
		* (name, isOptional)-> "crate::dora::platformer::UnitAction::from(#{name})#{isOptional and '' or '.unwrap()'}"
		* (fnArgId)-> "&crate::dora::platformer::UnitAction::from(stack#{fnArgId}.pop_i64().unwrap()).unwrap()"
		* -> error "unsupported"
	void: {}

lastPos = 1

Newline = Cmt P"\r"^-1 * P"\n", (str, pos)->
	lastPos = pos
	true

White = (S" \t" + Newline)^0

AlphaNum = R "az", "AZ", "09", "__"

Indent = R("az", "AZ", "__") * AlphaNum^0

Name = C R("az", "AZ", "__") * (AlphaNum + ":")^0

ClassName = C (Indent * White * P"::" * White)^0 * Indent

ClassLabel = C(P"singleton") + (C(P"interface")^-1 * White * C(P"object")) + C(P"value") + P""

FieldLabel = C(P"static")^-1 * White * C(P"optional")^-1 * White * C(P"readonly")^-1 * White * C(P"common" + P"boolean")^-1

MethodLabel = C(P"static")^-1 * White * C(P"outside")^-1 * White * C(P"optional")^-1

Type = C (Indent * White * P"::" * White)^0 * Indent * (White * P"*")^-1

Doc = C P"///" * (-Newline * P(1))^0

Docs = White * Ct (Doc * White)^0

mark = (name) -> (...) -> {name, ...}

Param = P {
	"Param"
	Param: V"Func" * White * Name / mark"callback" + Type * White * Name / mark"variable"
	Func: Ct P"function<" * White * Type * White * Ct P"(" * White * (V"Param" * (White * P"," * White * V"Param")^0 * White)^-1 * P")" * White * P">"
}

Method = Docs * Ct(White * MethodLabel) * White * Type * White * (C(P"operator==") + Name) * White * (P"@" * White * Name + Cc false) * White * Ct(P"(" * White * (Param * (White * P"," * White * Param)^0 * White)^-1 * P")") * White * C(P"const")^-1 * White * P";" / mark"method"

Field = Docs * Ct(White * FieldLabel) * White * Type * White * Name * White * (P"@" * White * Name + Cc false) * White * P";" / mark"field"
Class = White * Ct Docs * Ct(White * ClassLabel) * White * (P"class" + P"struct") * White * Ct(Name * White * (P"@" * White * Name + Cc false) * White * (P":" * White * (P"public")^-1 * White * ClassName * White + Cc false)) *
	P"{" *
		Ct (White * (Method + Field))^0 * White *
	P"}" * White * P";"

ModStart = White * P"namespace" * White * Name * White * P"{"
ModStop = White * P"}" * Cc false

File = Ct (ModStart + Class + ModStop)^1 * White * -1

codes = ""
with io.open "Dora.h", "r"
	codes = \read "*a"
	\close!

result = match File, codes

if not result
	if lastPos > #codes
		lastPos = #codes - 1

	line = 1
	begin = 0
	for i = 1, #codes
		break if i > lastPos
		if codes\sub(i, i) == '\n'
			line += 1
			begin = i

	following = codes\sub begin + 1
	lineStr = following\match("(.-)\n") or following\match ".*$"

	print "#{line}: syntax error:"
	print lineStr
else
	cppBinding = {}
	cppLink = {}
	rustExtern = {}
	rustBinding = []
	nameMap = {}
	toCamelCase = (name)-> table.concat [sub\lower! for sub in (name\sub(1, 1)\lower! .. name\sub(2))\gsub("%d", "_%1")\gsub("%u", "_%1")\gsub("(%d)_", "%1")\gmatch "[^_]*"], "_"
	getObjectType = (dataType, rustName, isCreate, isOptional)->
		if isCreate
			return
				* "i64"
				* (name)-> "r_cast<#{dataType}*>(#{name})"
				* (name)-> nonObjectTypes[rustName] and "r_cast<int64_t>(#{name})" or "from_object(#{name})"
				* "&#{rustName}"
				* isOptional and "Option<#{rustName}>" or rustName
				* (name)-> "#{name}.raw()"
				* (name)-> isOptional and "#{rustName}::from(#{name})" or "#{rustName} { raw: #{name} }"
				* -> error "unsupported"
				* -> error "unsupported"
		elseif isOptional
			return
				* "i64"
				* (name)-> "r_cast<#{dataType}*>(#{name})"
				* (name)-> nonObjectTypes[rustName] and "r_cast<int64_t>(#{name})" or "from_object(#{name})"
				* interfaces[rustName] and "&dyn crate::dora::I#{rustName}" or "&crate::dora::#{rustName}"
				* "Option<crate::dora::#{rustName}>"
				* (name)-> "#{name}.raw()"
				* (name)-> "crate::dora::#{rustName}::from(#{name})"
				* (fnArgId)-> "&stack#{fnArgId}.pop_cast::<crate::dora::#{rustName}>()"
				* (name, fnArgId)-> "stack#{fnArgId}.push_object(&#{name});"
		else
			return
				* "i64"
				* (name)-> "r_cast<#{dataType}*>(#{name})"
				* (name)-> nonObjectTypes[rustName] and "r_cast<int64_t>(#{name})" or "from_object(#{name})"
				* interfaces[rustName] and "&dyn crate::dora::I#{rustName}" or "&crate::dora::#{rustName}"
				* "crate::dora::#{rustName}"
				* (name)-> "#{name}.raw()"
				* (name)-> "crate::dora::#{rustName}::from(#{name}).unwrap()"
				* (fnArgId)-> "&stack#{fnArgId}.pop_cast::<crate::dora::#{rustName}>().unwrap()"
				* (name, fnArgId)-> "stack#{fnArgId}.push_object(&#{name});"
	moduleScopes = {}
	for cls in *result
		if "string" == type cls
			moduleScopes[] = cls
			continue
		elseif not cls
			table.remove moduleScopes, #moduleScopes
			continue
		[_, _, clsNames] = cls
		{clsName, clsNewName} = clsNames
		clsNewName or= clsName
		if #moduleScopes > 0
			moduleScope = table.concat moduleScopes, "::"
			rustModuleScope = table.concat [toCamelCase(mod) for mod in *moduleScopes], "::"
			nameMap[moduleScope .. "::" .. clsName] = rustModuleScope .. "::" .. clsNewName
		else
			nameMap[clsName] = clsNewName
	moduleScopes = {}
	clsCount = 0
	funcCount = 0
	for cls in *result
		if "string" == type cls
			moduleScopes[] = cls
			continue
		elseif not cls
			table.remove moduleScopes, #moduleScopes
			continue
		clsCount += 1
		namespace = if #moduleScopes > 0
			table.concat([toCamelCase(mod) for mod in *moduleScopes], "_") .. "_"
		else
			""
		cppNamespace = if #moduleScopes > 0
			table.concat(moduleScopes, "::") .. "::"
		else
			""
		[clsDocs, clsLabels, clsNames, clsBody] = cls
		[clsName, clsNewName, clsParent] = clsNames
		if clsParent
			clsParent = nameMap[clsParent] or clsParent
		clsNewName or= clsName
		isSingleton = false
		isObject = false
		isValue = false
		isTrait = false
		singletonName = nil
		for label in *clsLabels
			switch label
				when "singleton"
					isSingleton = true
					singletonName = "Shared#{clsName}"
				when "object"
					isObject = true
				when "value"
					isValue = true
				when "interface"
					isTrait = true
		moduleName = toCamelCase(clsNewName)\gsub("_(%l)_", "%1")\gsub "_(%l)$", "%1"
		clsNameL = clsName\lower!
		clsNewNameL = clsNewName\lower!
		cppModuleName = clsName
		clsName = cppNamespace .. clsName
		rustBinding[] = "use crate::dora::IObject;"
		objectUsed = #rustBinding
		if isObject
			objectUsed = nil
			cppBinding[] = "static int32_t #{namespace}#{clsNewNameL}_type() {
	return DoraType<#{clsName}>();
}"
		if clsParent
			rustBinding[] = "use crate::dora::#{clsParent};"
			rustBinding[] = "impl #{clsParent} for #{clsNewName} { }"
		elseif isObject
			clsParent = "IObject"
		if clsParent
			if inherits := inheritances[clsParent]
				for inherit in *inherits
					rustBinding[] = "use crate::dora::#{inherit};"
					rustBinding[] = "impl #{inherit} for #{clsNewName} { }"
		cppLink[] = "static void link#{table.concat moduleScopes}#{cppModuleName}(wasm3::module3& mod) {"
		if #clsDocs > 0
			rustBinding[] = table.concat clsDocs, '\n'
		rustBinding[] = if isTrait
			if isObject
				rustBinding[] = "pub struct #{clsNewName} { raw: i64 }
crate::dora_object!(#{clsNewName});
impl I#{clsNewName} for #{clsNewName} { }"
				cppLink[] = "\tmod.link_optional(\"*\", \"#{namespace}#{clsNewNameL}_type\", #{namespace}#{clsNewNameL}_type);"
			"pub trait I#{clsNewName}#{clsParent and ': ' .. clsParent or ''} {"
		else
			"pub struct #{clsNewName} { #{isSingleton and '' or 'raw: i64 '}}"
		if isValue
			rustBinding[] = "impl Drop for #{clsNewName} {
	fn drop(&mut self) { unsafe { #{namespace}#{clsNewNameL}_release(self.raw); } }
}"
		if isObject and not isTrait
			rustBinding[] = "crate::dora_object!(#{clsNewName});"
			cppLink[] = "\tmod.link_optional(\"*\", \"#{namespace}#{clsNewNameL}_type\", #{namespace}#{clsNewNameL}_type);"
		unless isTrait
			rustBinding[] = "impl #{clsNewName} {"
		rustExtern[] = "extern \"C\" {"
		if isObject
			rustExtern[] = "\tfn #{namespace}#{clsNewNameL}_type() -> i32;"
			rustBinding[] = "\tpub(crate) fn type_info() -> (i32, fn(i64) -> Option<Box<dyn IObject>>) {
		(unsafe { #{namespace}#{clsNewNameL}_type() }, |raw: i64| -> Option<Box<dyn IObject>> {
			match raw {
				0 => None,
				_ => Some(Box::new(#{clsNewName} { raw: raw }))
			}
		})
	}"
		if isValue
			cppBinding[] = "static void #{namespace}#{clsNewNameL}_release(int64_t raw) {
	delete r_cast<#{clsName}*>(raw);
}"
			rustExtern[] = "\tfn #{namespace}#{clsNewNameL}_release(raw: i64);"
			rustBinding[] = "\tpub(crate) fn raw(&self) -> i64 {
		self.raw
	}
	pub(crate) fn from(raw: i64) -> #{clsNewName} {
		#{clsNewName} { raw: raw }
	}"
			cppLink[] = "\tmod.link_optional(\"*\", \"#{namespace}#{clsNewNameL}_release\", #{namespace}#{clsNewNameL}_release);"
		if #clsLabels == 0
			rustBinding[] = "\tpub(crate) fn from(raw: i64) -> Option<#{clsNewName}> {
		match raw {
			0 => None,
			_ => Some(#{clsNewName} { raw: raw })
		}
	}
	pub(crate) fn raw(&self) -> i64 { self.raw }"
		for clsItem in *clsBody
			pub = isTrait and "" or "pub "
			[itemType,] = clsItem
			switch itemType
				when "field"
					[_, docs, labels, dataType, name, newName] = clsItem
					funcName = name\sub(1, 1)\lower! .. name\sub 2
					funcName = table.concat [sub\lower! for sub in funcName\gsub("%u", "_%1")\gmatch "[^_]*"], "_"
					funcNewName = newName or name
					funcNewName = funcNewName\sub(1, 1)\lower! .. funcNewName\sub 2
					funcNewName = table.concat [sub\lower! for sub in funcNewName\gsub("%u", "_%1")\gmatch "[^_]*"], "_"
					isReadonly = false
					isStatic = false
					isOptional = false
					fieldType = "primitive"
					for label in *labels
						switch label
							when "readonly"
								isReadonly = true
							when "common", "boolean"
								fieldType = label
							when "static"
								isStatic = true
							when "optional"
								isOptional = true
					funcCount += if isReadonly
						1
					else
						2
					if isStatic
						pub = if funcNewName\sub(1, 1) == "_"
							"pub(crate) "
						else
							"pub "
					elseif pub\sub(1, 3) == "pub"
						if funcNewName\sub(1, 1) == "_"
							pub = "pub(crate) "
					rustType = basicTypes[dataType]
					if rustType == nil and "*" == dataType\sub -1
						dataTypeName = dataType\match "[^ \t*]+"
						rustName = nameMap[dataTypeName] or dataTypeName
						rustType = getObjectType dataTypeName, rustName, false, isOptional
						if not isReadonly and not interfaces[rustName]
							objectUsed = nil
					if rustType
						[rt, cast, castFrom, rtTypeIn, rtTypeOut, rtCast, rtCastFrom] = rustType
						cppType = cppTypes[rt]
						if not isReadonly
							docText = if #docs > 0
								text = table.concat docs, '\n\t'
								'\t' .. text\sub(1, 4) .. 'Sets' .. text\sub(4) .. '\n'
							else
								''
							setFunc = switch fieldType
								when "primitive"
									"#{name} = #{cast 'var'}"
								when "common", "boolean"
									if dataType == cppType
										"set#{name\sub(1, 1)\upper!}#{name\sub 2}(var)"
									else
										"set#{name\sub(1, 1)\upper!}#{name\sub 2}(#{cast 'var'})"
							cppSetterName = "#{namespace}#{clsNameL}_set_#{funcNewName}"
							rustSetterName = "set_#{funcNewName}"
							cppLink[] = "\tmod.link_optional(\"*\", \"#{cppSetterName}\", #{cppSetterName});"
							cppBinding[] = if isSingleton
								callFunc = isStatic and "#{clsName}::#{setFunc};" or "#{singletonName}.#{setFunc};"
								"static void #{cppSetterName}(#{cppType} var) {\n\t#{callFunc}\n}"
							else
								"static void #{cppSetterName}(int64_t self, #{cppType} var) {\n\tr_cast<#{clsName}*>(self)->#{setFunc};\n}"
							rustExtern[] = if isSingleton
								"\tfn #{cppSetterName}(var: #{rt});"
							else
								slf = isStatic and "" or "slf: i64, "
								"\tfn #{cppSetterName}(#{slf}var: #{rt});"
							rustBinding[] = if isSingleton
								"#{docText}\t#{pub}fn #{rustSetterName}(var: #{rtTypeIn or rt}) {\n\t\tunsafe { #{cppSetterName}(#{rtCast and rtCast('var') or 'var'}) };\n\t}"
							else
								slfDecl = isStatic and "" or "&mut self, "
								slf = isStatic and "" or "self.raw(), "
								"#{docText}\t#{pub}fn #{rustSetterName}(#{slfDecl}var: #{rtTypeIn or rt}) {\n\t\tunsafe { #{cppSetterName}(#{slf}#{rtCast and rtCast('var') or 'var'}) };\n\t}"
						docText = if #docs > 0
							text = table.concat docs, '\n\t'
							'\t' .. text\sub(1, 4) .. 'Gets' .. text\sub(4) .. '\n'
						else
							''
						getFunc, prefix = switch fieldType
							when "primitive"
								"#{name}", "get"
							when "common"
								"get#{name\sub(1, 1)\upper!}#{name\sub 2}()", "get"
							when "boolean"
								"is#{name\sub(1, 1)\upper!}#{name\sub 2}()", "is"
						if dataType == cppType
							castFrom = (name)-> name
						cppGetterName = "#{namespace}#{clsNameL}_#{prefix}_#{funcNewName}"
						rustGetterName = "#{prefix}_#{funcNewName}"
						cppLink[] = "\tmod.link_optional(\"*\", \"#{cppGetterName}\", #{cppGetterName});"
						cppBinding[] = if isSingleton
							callFunc = isStatic and "#{clsName}::#{getFunc}" or "#{singletonName}.#{getFunc}"
							item = castFrom "#{callFunc}"
							"static #{cppType} #{cppGetterName}() {\n\treturn #{item};\n}"
						else
							callFunc = isStatic and "#{clsName}::#{getFunc}" or "r_cast<#{clsName}*>(self)->#{getFunc}"
							item = castFrom callFunc
							"static #{cppType} #{cppGetterName}(int64_t self) {\n\treturn #{item};\n}"
						rustExtern[] = if isSingleton
							"\tfn #{cppGetterName}() -> #{rt};"
						else
							slf = isStatic and "" or "slf: i64"
							"\tfn #{cppGetterName}(#{slf}) -> #{rt};"
						rustBinding[] = if isSingleton
							item = "#{cppGetterName}()"
							"#{docText}\t#{pub}fn #{rustGetterName}() -> #{rtTypeOut or rt} {\n\t\treturn unsafe { #{rtCastFrom and rtCastFrom(item) or item} };\n\t}"
						else
							slfDecl = isStatic and "" or "&self"
							slf = isStatic and "" or "self.raw()"
							item = "#{cppGetterName}(#{slf})"
							"#{docText}\t#{pub}fn #{rustGetterName}(#{slfDecl}) -> #{rtTypeOut or rt} {\n\t\treturn unsafe { #{rtCastFrom and rtCastFrom(item) or item} };\n\t}"
					else
						error "\"#{dataType}\" is not supported."
				when "method"
					funcCount += 1
					[_, docs, labels, dataType, name, newName, args, constFlag] = clsItem
					funcNewName = newName or name
					funcName = name\sub(1, 1)\lower! .. name\sub 2
					funcName = table.concat [sub\lower! for sub in funcName\gsub("%u", "_%1")\gmatch "[^_]*"], "_"
					isCreate = false
					if "create" == funcNewName\sub 1, 6
						isCreate = true
						funcNewName = if #funcNewName > 6
							"with" .. funcNewName\sub 7
						else
							"new"
						pub = "pub "
					funcNewName = funcNewName\sub(1, 1)\lower! .. funcNewName\sub 2
					funcNewName = table.concat [sub\lower! for sub in funcNewName\gsub("%u", "_%1")\gmatch "[^_]*"], "_"
					isStatic = false
					isOutside = false
					isOptional = false
					isConst = constFlag == "const"
					for label in *labels
						switch label
							when "outside"
								isOutside = true
							when "static"
								isStatic = true
							when "optional"
								isOptional = true
					if isStatic
						pub = if funcNewName\sub(1,1) == "_"
							"pub(crate) "
						else
							"pub "
					elseif pub\sub(1, 3) == "pub"
						if funcNewName\sub(1,1) == "_"
							pub = "pub(crate) "
					rustType = basicTypes[dataType]
					if rustType == nil and "*" == dataType\sub -1
						dataTypeName = dataType\match "[^ \t*]+"
						rustName = nameMap[dataTypeName] or dataTypeName
						rustType = getObjectType dataTypeName, rustName, isCreate, isOptional
					if rustType
						[rt, cast, castFrom, _, rtTypeOut, _, rtCastFrom] = rustType
						if isOptional and not rtTypeOut\match "^Option<"
							rtTypeOut = "Option<#{rtTypeOut}>"
						cppType = cppTypes[rt] ?? "void"
						funcArgCount = -1
						argItems = for arg in *args
							[argKind, argType, argName] = arg
							argName = toCamelCase argName
							switch argKind
								when "variable"
									rustTypeA = basicTypes[argType]
									if rustTypeA == nil and "*" == argType\sub -1
										argTypeName = argType\match "[^ \t*]+"
										rustName = nameMap[argTypeName] or argTypeName
										rustTypeA = getObjectType argTypeName, rustName
										objectUsed = nil unless interfaces[rustName]
									if rustTypeA
										[rtA, castA, _, rtTypeInA, _, rtCastA] = rustTypeA
										cppTypeA = cppTypes[rtA]
										[
											"#{cppTypeA} #{argName}"
											(argType == cppTypeA and argName or castA argName)
											"#{argName}: #{rtA}"
											"#{argName}: #{rtTypeInA or rtA}"
											"#{rtCastA and rtCastA(argName) or argName}"
										]
									else
										error "\"#{argType}\" is not supported."
								when "callback"
									funcArgCount += 1
									fnArgId = funcArgCount == 0 and '' or tostring funcArgCount
									[_, func, fname] = arg
									fname = toCamelCase fname
									[freturnType, fargs] = func
									items = for farg in *fargs
										[fargKind, fargType, fargName] = farg
										if fargKind ~= "variable"
											error "\"#{fargKind}\" is not supported in function argument."
										frustType = basicTypes[fargType]
										if frustType == nil and "*" == fargType\sub -1
											fargTypeName = fargType\match "[^ \t*]+"
											frustName = nameMap[fargTypeName] or fargTypeName
											frustType = getObjectType fargTypeName, frustName
										if frustType
											{frt, _2, _3, frtTypeIn, _5, _6, _7, fpopArg} = frustType
											fcppType = callbackDefs[fargType] or fargType
											switch fargType\match "[^ \t*]+"
												when "Event"
													[
														"Event* #{fargName}"
														"\t\t#{fargName}->pushArgsToWasm(args#{fnArgId});"
														"&mut crate::dora::CallStack"
														"&mut stack#{fnArgId}"
													]
												when "Platformer::Behavior::Blackboard", "Platformer::UnitAction"
													[
														"#{fcppType}* #{fargName}"
														"\t\targs#{fnArgId}->push(r_cast<int64_t>(#{fargName}));"
														frtTypeIn
														fpopArg fnArgId
													]
												when "DBRecord"
													[
														"#{fcppType}& #{fargName}"
														"\t\targs#{fnArgId}->push(r_cast<int64_t>(new DBRecord{std::move(#{fargName})}));"
														frtTypeIn
														fpopArg fnArgId
													]
												when "MLQState", "MLQAction"
													[
														"#{fcppType} #{fargName}"
														"\t\targs#{fnArgId}->push(s_cast<int64_t>(#{fargName}));"
														frtTypeIn
														fpopArg fnArgId
													]
												when nil
													error "\"#{fargType}\" is not supported."
												else
													[
														"#{fcppType} #{fargName}"
														"\t\targs#{fnArgId}->push(#{fargName});"
														frtTypeIn
														fpopArg fnArgId
													]
									argPassed = #items > 0
									cppArgDef = table.concat [item[1] for item in *items], ", "
									cppArgPass = table.concat [item[2] for item in *items], "\n"
									if cppArgPass ~= ""
										cppArgPass = "#{cppArgPass}\n"
									callbackType = "dyn FnMut(#{table.concat [item[3] for item in *items], ', '})"
									callback = "#{fname}(#{table.concat [item[4] for item in *items], ', '})"
									callbackReturn = ""
									frRetType = basicTypes[freturnType]
									if frRetType == nil and "*" == freturnType\sub -1
										freturnType = freturnType\sub 1, -2
										rustName = nameMap[freturnType] or freturnType
										frRetType = getObjectType freturnType, rustName
									if frRetType
										{frt, _2, _3, _4, frtTypeOut, _6, _7, _8, frPush} = frRetType
										callbackType = if frtTypeOut
											"Box<#{callbackType} -> #{frtTypeOut}>"
										else
											"Box<#{callbackType}>"
										if frCppType := cppTypes[frt]
											callbackReturn = switch freturnType
												when "Node"
													"\t\treturn s_cast<Node*>(std::get<Object*>(args#{fnArgId}->pop()));\n"
												when "string"
													"\t\treturn std::get<std::string>(args#{fnArgId}->pop());\n"
												when "Platformer::WasmActionUpdate"
													"\t\treturn *r_cast<Platformer::WasmActionUpdate*>(std::get<int64_t>(args#{fnArgId}->pop()));\n"
												else
													"\t\treturn std::get<#{freturnType}>(args#{fnArgId}->pop());\n"
											callback = "let result = #{callback};\n\t\t\t#{frPush 'result', fnArgId}"
											argPassed = true
									{
										"int32_t func#{fnArgId}#{argPassed and ', int64_t stack' .. fnArgId or ''}"
										"[func#{fnArgId}, #{argPassed and 'args' .. fnArgId .. ', ' or ''}deref#{fnArgId}](#{cppArgDef}) {
#{argPassed and '\t\targs' .. fnArgId .. '->clear();\n' .. cppArgPass or ''}\t\tSharedWasmRuntime.invoke(func#{fnArgId});
#{callbackReturn}	}"
										"func#{fnArgId}: i32#{argPassed and ', stack' .. fnArgId .. ': i64' or ''}"
										"mut #{fname}: #{callbackType}"
										"func_id#{fnArgId}#{argPassed and ', stack_raw' .. fnArgId or ''}"
										"
	std::shared_ptr<void> deref#{fnArgId}(nullptr, [func#{fnArgId}](auto) {
		SharedWasmRuntime.deref(func#{fnArgId});
	});#{argPassed and '\n\tauto args' .. fnArgId .. ' = r_cast<CallStack*>(stack' .. fnArgId .. ');' or ''}"
										(argPassed and "\t\tlet mut stack#{fnArgId} = crate::dora::CallStack::new();
		let stack_raw#{fnArgId} = stack#{fnArgId}.raw();\n" or '') .. "\t\tlet func_id#{fnArgId} = crate::dora::push_function(Box::new(move || {
			#{callback}
		}));
"
									}
						argDefs = table.concat [item[1] for item in *argItems], ", "
						argPass = table.concat [item[2] for item in *argItems], ", "
						argRtDefs = table.concat [item[3] for item in *argItems], ", "
						argRtInDefs = table.concat [item[4] for item in *argItems], ", "
						argRtPass = table.concat [item[5] for item in *argItems], ", "
						argPrepare = table.concat [item[6] for item in *argItems when item[6]?]
						argRtPrepare = table.concat [item[7] for item in *argItems when item[7]?]
						if dataType == cppType
							castFrom = (name)-> name
						prefix = if cppType == "void" then "" else "return "
						cppFuncName = "#{namespace}#{clsNameL}_#{funcNewName}"
						rustFuncName = funcNewName
						cppLink[] = "\tmod.link_optional(\"*\", \"#{cppFuncName}\", #{cppFuncName});"
						cppBinding[] = if isSingleton
							callFunc = isStatic and "#{clsName}::#{name}(#{argPass})" or "#{singletonName}.#{name}(#{argPass})"
							callFunc = isOutside and "#{name}(#{argPass})" or callFunc
							item = castFrom callFunc
							"static #{cppType} #{cppFuncName}(#{argDefs}) {#{argPrepare}\n\t#{prefix}#{item};\n}"
						else
							slf = isStatic and "" or "int64_t self"
							if slf ~= "" then argDefs = slf .. (argDefs ~= "" and ", " or "") .. argDefs
							callFunc = if isOutside
								if isStatic
									"#{name}(#{argPass})"
								else
									"#{name}(r_cast<#{clsName}*>(self)#{argPass == '' and '' or ', ' .. argPass})"
							else
								if isStatic
									"#{clsName}::#{name}(#{argPass})"
								else
									"r_cast<#{clsName}*>(self)->#{name}(#{argPass})"
							callFunc = isValue and argPass or callFunc if isCreate
							item = castFrom callFunc
							"static #{cppType} #{cppFuncName}(#{argDefs}) {#{argPrepare}\n\t#{prefix}#{item};\n}"
						rustExtern[] = if isSingleton
							"\tfn #{cppFuncName}(#{argRtDefs})#{rt and ' -> ' .. rt or ''};"
						else
							slf = isStatic and "" or "slf: i64"
							if slf ~= "" then argRtDefs = slf .. (argRtDefs ~= "" and ", " or "") .. argRtDefs
							"\tfn #{cppFuncName}(#{argRtDefs})#{rt and ' -> ' .. rt or ''};"
						rustBinding[] = if isSingleton
							rtOut = rtTypeOut or rt
							item = "#{cppFuncName}(#{argRtPass})"
							docText = if #docs > 0
								'\t' .. table.concat(docs, '\n\t') .. '\n'
							else
								''
							"#{docText}\t#{pub}fn #{rustFuncName}(#{argRtInDefs})#{rtOut and ' -> ' .. rtOut or ''} {\n#{argRtPrepare}\t\tunsafe { #{prefix}#{rtCastFrom and rtCastFrom(item, isOptional) or item}; }\n\t}"
						else
							rtOut = rtTypeOut or rt
							slfParam = isConst and "&self" or "&mut self"
							slfDecl = isStatic and "" or slfParam
							if slfDecl ~= "" then argRtInDefs = slfDecl .. (argRtInDefs ~= "" and ", " or "") .. argRtInDefs
							slf = isStatic and "" or "self.raw()"
							if slf ~= "" then argRtPass = slf .. (argRtPass ~= "" and ", " or "") .. argRtPass
							item = "#{cppFuncName}(#{argRtPass})"
							if funcNewName == "equals"
								docText = if #docs > 0
									'\t' .. table.concat docs, '\n\t'
								else
									''
								table.insert rustBinding, objectUsed and objectUsed + 1 or 2, "impl PartialEq for Rect {
#{docText}
	fn eq(&self, other: &Self) -> bool {\n#{argRtPrepare}\t\tunsafe { #{prefix}#{rtCastFrom and rtCastFrom(item, isOptional) or item} }\n\t}
}"
								nil
							else
								docText = if #docs > 0
									'\t' .. table.concat(docs, '\n\t') .. '\n'
								else
									''
								"#{docText}\t#{pub}fn #{rustFuncName}(#{argRtInDefs})#{rtOut and ' -> ' .. rtOut or ''} {\n#{argRtPrepare}\t\tunsafe { #{prefix}#{rtCastFrom and rtCastFrom(item, isOptional) or item}; }\n\t}"
					else
						error "\"#{dataType}\" is not supported."
		if objectUsed
			table.remove rustBinding, objectUsed
		rustExtern[] = "}"
		if isTrait
			isStaticFunc = (item)-> item\match("fn ") and not item\match("&self") and not item\match("&mut self")
			createFuncs = [item for item in *rustBinding when isStaticFunc item]
			rustBinding = [item for item in *rustBinding when not isStaticFunc item]
			if #createFuncs > 0
				rustBinding[] = "}"
				rustBinding[] = "impl #{clsNewName} {"
				for func in *createFuncs
					rustBinding[] = func
		rustBinding[] = "}"
		cppLink[] = "}"
		moduleScope = table.concat moduleScopes, "/"
		close _f1 = with io.open "../../Source/Wasm/Dora/#{moduleScope ~= '' and moduleScope .. "/" .. cppModuleName or cppModuleName}Wasm.hpp", "w"
			\write licenseText
			\write "\n\n"
			\write table.concat cppBinding, "\n"
			\write "\n"
			\write table.concat cppLink, "\n"
		moduleScope = table.concat [toCamelCase(mod) for mod in *moduleScopes], "/"
		close _f2 = with io.open "../dora-rust/dora/src/dora/#{moduleScope ~= '' and moduleScope .. "/" .. moduleName or moduleName}.rs", "w"
			\write licenseText
			\write "\n\n"
			\write table.concat rustExtern, "\n"
			\write "\n"
			\write table.concat rustBinding, "\n"
		rustExtern = {}
		rustBinding = {}
		cppBinding = {}
		cppLink = {}
	print "#{clsCount} classes, #{funcCount} functions Done!"

