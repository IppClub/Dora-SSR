#!yue -e

--[[ Copyright (c) 2024 Li Jin, dragon-fly@qq.com

Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE. ]]

bgfx = "/Users/Jin/Workspace/bgfx"
dora = "/Users/Jin/Workspace/Dora-SSR"
tool = "#{bgfx}/tools/bin/darwin/shaderc"
path = "#{dora}/Source/Shader"

essl = "300_es"
glsl = "130"
spirv = "spirv"
metal = "metal"

efkbgfx =
	sprite:
		Unlit:
			vs: "sprite_unlit_vs"
			fs: "model_unlit_ps"
		Lit:
			vs: "sprite_lit_vs"
			fs: "model_lit_ps"
		BackDistortion:
			vs: "sprite_distortion_vs"
			fs: "model_distortion_ps"
		AdvancedUnlit:
			vs: "ad_sprite_unlit_vs"
			fs: "ad_model_unlit_ps"
		AdvancedLit:
			vs: "ad_sprite_lit_vs"
			fs: "ad_model_lit_ps"
		AdvancedBackDistortion:
			vs: "ad_sprite_distortion_vs"
			fs: "ad_model_distortion_ps"
	model:
		Unlit:
			vs: "model_unlit_vs"
			fs: "model_unlit_ps"
		Lit:
			vs: "model_lit_vs"
			fs: "model_lit_ps"
		BackDistortion:
			vs: "model_distortion_vs"
			fs: "model_distortion_ps"
		AdvancedUnlit:
			vs: "ad_model_unlit_vs"
			fs: "ad_model_unlit_ps"
		AdvancedLit:
			vs: "ad_model_lit_vs"
			fs: "ad_model_lit_ps"
		AdvancedBackDistortion:
			vs: "ad_model_distortion_vs"
			fs: "ad_model_distortion_ps"

compiled = {}
for modeltype, shaders in pairs efkbgfx do for shadertype, shader in pairs shaders do for stage in *{"v", "f"}
	sname = shader[stage .. "s"]
	continue if compiled[sname]
	compiled[sname] = true
	dir = "#{path}/efkbgfx"
	file = "#{sname}.fx.sc"
	target = "#{dir}/#{sname}.bin.h"
	with io.popen ": > #{dir}/#{sname}.bin.h" --"printf \"extern const uint8_t* #{sname}_pssl;\\nextern const uint32_t #{sname}_pssl_size;\\n\" > #{dir}/#{sname}.bin.h"
		result = \read "a"
		\close!
		print result if result ~= ""
	for {cmd, t} in *{
			{"#{tool} -i #{dir} -i #{bgfx}/src --type #{stage} --platform android -f #{dir}/#{file} -o #{dir}/tmp -p #{essl} -O 3 --bin2c #{sname}_essl --varyingdef #{dir}/#{modeltype}_#{shadertype}_varying.def.sc", "OpenGLES"}
			{"#{tool} -i #{dir} -i #{bgfx}/src --type #{stage} --platform osx -f #{dir}/#{file} -o #{dir}/tmp -p #{glsl} -O 3 --bin2c #{sname}_glsl --varyingdef #{dir}/#{modeltype}_#{shadertype}_varying.def.sc", "OpenGL"}
			{"#{tool} -i #{dir} -i #{bgfx}/src --type #{stage} --platform linux -f #{dir}/#{file} -o #{dir}/tmp -p #{spirv} -O 3 --bin2c #{sname}_spv --varyingdef #{dir}/#{modeltype}_#{shadertype}_varying.def.sc", "Spirv"}
			{"#{tool} -i #{dir} -i #{bgfx}/src --type #{stage} --platform osx -f #{dir}/#{file} -o #{dir}/tmp -p #{metal} -O 3 --bin2c #{sname}_mtl --varyingdef #{dir}/#{modeltype}_#{shadertype}_varying.def.sc", "Metal"}
		}
		print "#{dir}/#{file} #{t} Building."
		with io.popen cmd
			result = \read "a"
			\close!
			if result == ""
				print "#{dir}/#{file} #{t} Built."
			else
				print result
				print "#{dir}/#{file} #{t} Failed."
				return
		with io.popen "cat #{dir}/tmp >> #{target}"
			result = \read "a"
			\close!
			if result ~= ""
				print result
				return
		with io.popen "rm -f #{dir}/tmp"
			\close!

for stage in *{"v", "f"}
	pathStr = io.popen("fselect dir,name from #{path} where name like '#{stage}s_%.sc'")\read "a"
	for dir, file in pathStr\gmatch "([^\t]+)\t([^\n]+)\n?"
		sname = file\match "[^%.]+"
		target = "#{dir}/#{sname}.bin.h"
		with io.popen ": > #{dir}/#{sname}.bin.h" --"printf \"extern const uint8_t* #{sname}_pssl;\\nextern const uint32_t #{sname}_pssl_size;\\n\" > #{dir}/#{sname}.bin.h"
			result = \read "a"
			\close!
			if result ~= ""
				print result
				return
		for {cmd, t} in *{
				{"#{tool} -i #{bgfx}/src --type #{stage} --platform android -f #{dir}/#{file} -o #{dir}/tmp -p #{essl} -O 3 --bin2c #{sname}_essl --varyingdef #{dir}/varying.def.sc", "OpenGLES"}
				{"#{tool} -i #{bgfx}/src --type #{stage} --platform osx -f #{dir}/#{file} -o #{dir}/tmp -p #{glsl} -O 3 --bin2c #{sname}_glsl --varyingdef #{dir}/varying.def.sc", "OpenGL"}
				{"#{tool} -i #{bgfx}/src --type #{stage} --platform linux -f #{dir}/#{file} -o #{dir}/tmp -p #{spirv} -O 3 --bin2c #{sname}_spv --varyingdef #{dir}/varying.def.sc", "Spirv"}
				{"#{tool} -i #{bgfx}/src --type #{stage} --platform osx -f #{dir}/#{file} -o #{dir}/tmp -p #{metal} -O 3 --bin2c #{sname}_mtl --varyingdef #{dir}/varying.def.sc", "Metal"}
			}
			print "#{dir}/#{file} #{t} Building."
			with io.popen cmd
				result = \read "a"
				\close!
				if result == ""
					print "#{dir}/#{file} #{t} Built."
				else
					print result
					print "#{dir}/#{file} #{t} Failed."
					return
			with io.popen "cat #{dir}/tmp >> #{target}"
				result = \read "a"
				\close!
				if result ~= ""
					print result
					return
			with io.popen "rm -f #{dir}/tmp"
				\close!


