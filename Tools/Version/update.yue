#!yue -e

--[[ Copyright (c) 2024 Li Jin, dragon-fly@qq.com

Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE. ]]

version = "0.0.0"
with io.open "../../Source/Basic/Application.cpp", "rb"
	content = \read "*a"
	\close!
	version = content\match "#define DORA_VERSION \"([^\"]+)\""

for platform in *{"macOS", "iOS"}
	with io.open "../../Projects/#{platform}/Dora/Info.plist", "rb"
		content = \read "*a"
		\close!
		content = content\gsub "(<key>CFBundleShortVersionString</key>%s+<string>)[^<]+(</string>)", "%1#{version}%2"
		bundleVersion = tonumber content\match "<key>CFBundleVersion</key>%s+<string>([^<]+)</string>"
		content = content\gsub "(<key>CFBundleVersion</key>%s+<string>)[^<]+(</string>)", "%1#{bundleVersion + 1}%2"
		with io.open "../../Projects/#{platform}/Dora/Info.plist", "w+"
			\write content
			\close!

with io.open "../../Projects/Windows/Dora/Resource.rc", "rb"
	content = \read "*a"
	\close!
	content = content\gsub "(FILEVERSION )[^\n]+(\n)", "%1#{version\gsub '%.', ','},0%2"
	content = content\gsub "(PRODUCTVERSION )[^\n]+(\n)", "%1#{version\gsub '%.', ','},0%2"
	content = content\gsub "(VALUE \"FileVersion\", \")[^\"]+(\")", "%1#{version}.0%2"
	content = content\gsub "(VALUE \"ProductVersion\", \")[^\"]+(\")", "%1#{version}.0%2"
	with io.open "../../Projects/Windows/Dora/Resource.rc", "w+"
		\write content
		\close!

with io.open "../../Projects/Android/Dora/app/build.gradle", "r"
	content = \read "*a"
	\close!
	content = content\gsub "(versionName \")[^\"]+(\")", "%1#{version}%2"
	bundleVersion = tonumber content\match "versionCode ([^\n]+)\n"
	content = content\gsub "(versionCode )[^\n]+(\n)", "%1#{bundleVersion + 1}%2"
	with io.open "../../Projects/Android/Dora/app/build.gradle", "w+"
		\write content
		\close!

print "version updated to #{version}"
