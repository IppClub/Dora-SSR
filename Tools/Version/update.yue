#!yue -e

version = "0.0.0"
with io.open "../../Source/Basic/Application.cpp", "rb"
	content = \read "*a"
	\close!
	version = content\match "#define DORA_VERSION \"([^\"]+)\""

for platform in *{"macOS", "iOS"}
	with io.open "../../Project/#{platform}/Dora/Info.plist", "rb"
		content = \read "*a"
		\close!
		content = content\gsub "(<key>CFBundleShortVersionString</key>%s+<string>)[^<]+(</string>)", "%1#{version}%2"
		bundleVersion = tonumber content\match "<key>CFBundleVersion</key>%s+<string>([^<]+)</string>"
		content = content\gsub "(<key>CFBundleVersion</key>%s+<string>)[^<]+(</string>)", "%1#{bundleVersion + 1}%2"
		with io.open "../../Project/#{platform}/Dora/Info.plist", "w+"
			\write content
			\close!

with io.open "../../Project/Windows/Dora/Resource.rc", "rb"
	content = \read "*a"
	\close!
	content = content\gsub "(FILEVERSION )[^\n]+(\n)", "%1#{version\gsub '%.', ','},0%2"
	content = content\gsub "(PRODUCTVERSION )[^\n]+(\n)", "%1#{version\gsub '%.', ','},0%2"
	content = content\gsub "(VALUE \"FileVersion\", \")[^\"]+(\")", "%1#{version}.0%2"
	content = content\gsub "(VALUE \"ProductVersion\", \")[^\"]+(\")", "%1#{version}.0%2"
	with io.open "../../Project/Windows/Dora/Resource.rc", "w+"
		\write content
		\close!

with io.open "../../Project/Android/app/build.gradle", "r"
	content = \read "*a"
	\close!
	content = content\gsub "(versionName \")[^\"]+(\")", "%1#{version}%2"
	bundleVersion = tonumber content\match "versionCode ([^\n]+)\n"
	content = content\gsub "(versionCode )[^\n]+(\n)", "%1#{bundleVersion + 1}%2"
	with io.open "../../Project/Android/app/build.gradle", "w+"
		\write content
		\close!

print "version updated to #{version}"
